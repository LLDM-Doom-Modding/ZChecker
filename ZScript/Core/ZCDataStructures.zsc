//
// All what is dedicated to store (and maybe to slightly handle) the data.
//

// Class used to transfer the data between "NetworkProcess()", console commands manager and CCMDs itselfs.
class ZCheckerCCMDInfo play {
	const CCMDINFO_PARAMETERS_SEPARATOR = "##";

	Array<String> args;			// Arguments excluding ZC-CCMD name;
	ZCheckerHandler handler;	// Pointer to the main StaticEventHandler (some GZDoom versions strangely crashes on the "FindEventHandler()" calls);
	transient ConsoleEvent e;	// Pointer to the pure and non-altered structure.

	bool paramNoUsage;
	bool paramCallHelp;
	int paramLogLevelOverride;

	int maxArgsAmount;
	Array<ZCConsoleCommand> nonblockingCommands;
	ZCConsoleCommand usageNonblockingCommand;


	private void handleSpecialParameters( void ) {
		Array<String> parameters;	// First-argument CCMD special parameters.
		parameters.Clear();

		args[ 0 ].ToLower();
		args[ 0 ].Split( parameters, CCMDINFO_PARAMETERS_SEPARATOR, TOK_SKIPEMPTY );

		for ( int i = 0; i < parameters.Size(); i++ ) {
			String curparam = parameters[ i ];
			int curparamlen = curparam.Length();

			ZCGlobal.Log( LL_Debug, String.Format( "CCMD parameter #%i: \"%s\".", i, parameters[ i ] ) );

			if ( curparam == "nousage" || curparam == "quiet" ) {
				paramNoUsage = true;

			} else if ( ( curparamlen == 10 && curparam.Left( 8 ) == "loglevel" ) || ( curparamlen == 4 && curparam.Left( 2 ) == "ll" ) ) {
				String lastchar = curparam.Mid( curparamlen - 1, 1 );
				int tempConverted = lastchar.ToInt();

				if ( tempConverted != 0 || lastchar == '0' ) {
					paramLogLevelOverride = tempConverted;
				} else {
					ZCGlobal.Log( LL_Main, String.Format(
									LLCOLOR_WARNING .. "Warning\c-: wrong log level \"%s\" at position %i, must be <%i..%i>.",
									curparam, i, LL_Emergency, LL_Debug
								) );
				}

			} else if ( curparam == "debug" ) {
				paramLogLevelOverride = LL_Debug;

			} else {
				ZCGlobal.Log( LL_Main, String.Format(
								LLCOLOR_WARNING .. "Warning\c-: unknown CCMD parameter \"%s\" at position %i.",
								curparam, i
							) );
				break;
			}

		} // of for ( int i = 0; i < parameters.Size(); i++ ) {}

	} // of private void handleSpecialParameters( void ) {}


	// Init or reinit:
	String InitFromEvent( ConsoleEvent initEvent ) {
		args.Clear();
		maxArgsAmount = 0;
		int curLoglevel = ZCGlobal.GetLoglevel();

		e = initEvent;
		e.Name.Split( args, ZCHECKER_AGRS_SEPARATOR, TOK_SKIPEMPTY );

		// If debug is enabled, display maximal amount of arguments (sometimes 
		//it saves about ten minutes of the bughunting...):
		if ( curLoglevel >= LL_APIMessage ) {
			int curpos = 0;
			int separatorStrLen = ZCHECKER_AGRS_SEPARATOR.Length();

			while ( curpos < e.Name.Length() && -1 != ( curpos = e.Name.IndexOf( ZCHECKER_AGRS_SEPARATOR, curpos + separatorStrLen ) ) )
				maxArgsAmount++;

			//console.printf( "maxArgsAmount " .. maxArgsAmount .. " as \"" .. e.Name .. "\" index " .. e.Name.IndexOf( ZCHECKER_AGRS_SEPARATOR ) );
		}


		String zcCommandName = "";

		paramNoUsage = false;
		paramCallHelp = false;
		paramLogLevelOverride = curLoglevel;

		if ( args.Size() ) {
			// Get rid of the event name:
			zcCommandName = args[ 0 ];
			args.Delete( 0, 1 );

			if ( args.Size() ) {
				String arg0 = args[ 0 ];
				arg0.ToLower();

				if ( arg0.Left( 2 ) == CCMDINFO_PARAMETERS_SEPARATOR ) {
					handleSpecialParameters();
					args.Delete( 0, 1 );

				} else if ( arg0 == "help" || arg0 == "-h" || arg0 == "--help" ) {
					paramCallHelp = true;
				}
			} // of if ( args.Size() ) {}

		} else {
			ZCGlobal.ClearscopeLog( LL_Emergency, GetClassName() .. "::InitFromEvent(). Arguments is omitted." );
		}

		return zcCommandName;

	} // of String InitFromEvent( ConsoleEvent initEvent ) {}


	void SetUsageMessageNonblockingCCMD( ZCConsoleCommand ccmd ) {
		usageNonblockingCommand = ccmd;
	}
	ZCConsoleCommand GetUsageMessageNonblockingCCMD( void ) const {
		return usageNonblockingCommand;
	}
} // of class ZCheckerCCMDInfo play {}


// External interface group of the CCMD for "zchelp" and similar things.
//Also, ZChecker handler works in general only with this structure.
struct ZCheckerCCMDInterfaceData {
	// Linked netevent command name (see KEYCONF):
	String neteventName;

	// Header aliases separated by a semicolon ("zcadd [weap[on]|self|force] [<panel>]"):
	String helpHeaderLoczNodeName;
	String helpHeader;
	// Usage quick help (command description):
	String helpDescLoczNodeName;
	String helpDesc;

	// Internal help aliases ("zcadd;zcaddweap;zcaddself;zcaddforce"):
	Array<String> helpInternalAliases;

	// A tooltip class (extra information which is being printed via "zchelp <command>"):
	ZCTooltipClass helpTooltip;

	void Update( void ) {
		helpHeader = ZCGlobal.FreeLocalize( helpHeaderLoczNodeName );
		helpDesc = ZCGlobal.FreeLocalize( helpDescLoczNodeName );

		if ( helpTooltip )
			helpTooltip.ResetCachedHelpString();
	}
} // of struct ZCheckerCCMDInterfaceData {}


// A panel field storing object in question; right now only for Actors.
class ZCheckerField {
	enum EZCheckerActorType {
		IAT_Normal = 0,
		IAT_StateProvider
	};

	const const_MixStringsAppendLen = 4;


	private uint fieldPos;
	Actor mo;				// Testee actor itself.
	EZCheckerActorType actorType;

	uint asyncwith;			// Warn in multiplayer (0 == synced, 1..8 == player).
	String hashsync[ MAXPLAYERS ];

	play void UpdateActorMetainformation( ZCheckerCCMDInfo fccmd ) {
		if ( !mo )
			return;

		ZCGlobal.Get().SetClassnameVariable( "$last", mo.GetClassName() );
		fccmd.handler.UpdateInternalClassnameByField( fieldPos );
		UpdateHash( fccmd.e.Player );
	}

	int GetFieldPos( void ) {
		return fieldPos;
	}


	static ZCheckerField Create( int testeepos ) {
		ZCheckerField newfield = new( 'ZCheckerField' );

		newfield.fieldPos = testeepos;
		newfield.mo = NULL;
		newfield.actorType = IAT_Normal;
		newfield.asyncwith = 0;

		for ( int i = 0; i < MAXPLAYERS; i++ )
			newfield.hashsync[ i ] = "<Field " .. testeepos .. " hash " .. i .. " not initialized>";

		return newfield;
	}


	/* Multiplayer-related section */

	private String mixStrings( String a, String b ) {
		String outstr = "";
		int alen = a.Length();
		int blen = b.Length();

		if ( alen == 0 || blen == 0 ) {
			ZCGlobal.DataLog( LL_Emergency, GetClassName() .. "::mixStrings(). Zero length for string a:\"" .. TEXTCOLOR_WHITE .. a .. LLCOLOR_EMERGENCY .. "\" and/or b:\"" .. TEXTCOLOR_WHITE .. b .. LLCOLOR_EMERGENCY .. "\"." );
			return a;
		}

		// Adjusting string lengths:
		while ( alen > blen ) {
			b = b .. b.Left( min( blen, alen - blen ) );
			blen = b.Length();
		}
		while ( blen > alen ) {
			a = a .. a.Left( min( alen, blen - alen ) );
			alen = a.Length();
		}

		if ( alen != blen ) {
			ZCGlobal.DataLog( LL_Emergency, GetClassName() .. "::mixStrings(). Mismatch ajdusted string lengths: \"" .. TEXTCOLOR_WHITE .. a .. LLCOLOR_EMERGENCY .. "\" (" .. alen .. ") vs \"" .. TEXTCOLOR_WHITE .. b .. LLCOLOR_EMERGENCY .. "\" (" .. blen .. ")." );
			return a;
		}

		// Mixing (four chars appending is for about 1.8 times faster than every char-wise):
		for ( int i = 0; i < int( alen / const_MixStringsAppendLen ) * const_MixStringsAppendLen; ) {
			uint nextchars[ const_MixStringsAppendLen ];

			for ( int j = 0; j < const_MixStringsAppendLen; j++ ) {
				nextchars[ j ] = a.CharCodeAt( i ) ^ b.CharCodeAt( i );
				i++;

				if ( nextchars[ j ] == 0 )
					nextchars[ j ] = 32;
			}

			outstr.AppendFormat( "%c%c%c%c", nextchars[ 0 ], nextchars[ 1 ], nextchars[ 2 ], nextchars[ 3 ] );
		}

		for ( int i = 0; i < int( alen % const_MixStringsAppendLen ); i++ ) {
			int nextchar = a.CharCodeAt( i ) ^ b.CharCodeAt( i );

			if ( nextchar == 0 )
				nextchar = 32;

			outstr.AppendFormat( "%c", nextchar );
		}

		return outstr;
	} // of private String mixStrings( String a, String b ) {}

	String GetHash( void ) {
		if ( !mo )
			return "<No actor at " .. fieldPos .. ">";

		String outstr = mixStrings( mo.GetClassName(), String.Format( "%i%.2f%.2f%.2f%.2f", mo.tics + fieldPos * 10 + actorType * 100, mo.angle + mo.pitch, mo.pos.x, mo.pos.y, mo.pos.z ) );

		outstr = mixStrings( outstr, ( mo.inv? mo.inv.GetClassName() .. "" : "<No_inventory>" ) );
		outstr.Replace( ZCHECKER_AGRS_SEPARATOR, "a" );

		return outstr;
	}

	void UpdateHash( int playernum ) {
		hashsync[ playernum ] = GetHash();
	}
} // of class ZCheckerField {}


// Auxiliary class linking the panel parts and an associated CVars.
class ZCInfoPanelNode {
	const const_WrongCVar = -1;

	ZCBaseInfoPanelPart panel;
	transient CVar panelCVar;

	void InitCVar( void ) {
		panelCVar = CVar.GetCVar( panel.optionCVarName, players[ consoleplayer ] );
	}

	// "panelnumber" must be in range [0; MaxTestee - 1].
	bool EnabledForPanel( int panelnumber ) {
		bool outval = false;

		if ( panelCVar )
			outval = !!( panelCVar.GetInt() & ( 1 << panelnumber ) );

		return outval;
	}
}

// Convenience wrapper for all cases of presentation options of the panel nodes.
class ZCInfoPanelNodesKeeper {
	Array<ZCInfoPanelNode> nodes;
	Array<ZCInfoPanelNode> playnodes;

	void Push( ZCBaseInfoPanelPart saveepanel ) {
		ZCInfoPanelNode newnode = new( 'ZCInfoPanelNode' );
		newnode.panel = saveepanel;

		nodes.Push( newnode );

		if ( saveepanel.IsPlayScope() )
			playnodes.Push( newnode );
	}

	void Insert( uint index, ZCBaseInfoPanelPart saveepanel ) {
		ZCInfoPanelNode newnode = new( 'ZCInfoPanelNode' );
		newnode.panel = saveepanel;

		nodes.Insert( index, newnode );

		if ( saveepanel.IsPlayScope() )
			playnodes.Push( newnode );
	}

	class<ZCBaseInfoPanelPart> GetPanelClass( uint index ) {
		return nodes[ index ].panel.GetClass();
	}

	int Size( void ) {
		return nodes.Size();
	}
	int PlayscopedSize( void ) {
		return playnodes.Size();
	}

	void Clear( void ) {
		nodes.Clear();
		playnodes.Clear();
	}
} // of class ZCInfoPanelNodesKeeper {}

// Data-scoped panels storage and manager.
class ZCPanelsStorage {
	Array<ZCBaseInfoPanelPart> defaultPanels;	// Loaded from the ZChecker configuration file ("zchecker.cfg").
	ZCInfoPanelNodesKeeper curPanels;			// Loaded from the *.ini.

	// Panels order data:
	String panelsstr; 			// General I/O variable.
	bool panelsorderChanged; 	// Set it to "true" if a panels order must be saved.
	bool panelsResetRequest;	// Set it to "true" when all panels must be reinitialized (e.g. after language change).

	// Fonts data:
	transient Font infopanelsFont;
	int infopanelsFontSpaceWidth;

	// For the "[Init|Tick]SetPanelsorderCVar()":
	private transient CVar panelsOrderCVar;
	private String newOrderStr;
	private int newOrderStrPartsAmount;
	private bool newOrderSaveRequest;

	// For the "UpdateFont()":
	private transient CVar fonttypeCVar;
	private int lastFontType;

	// As "254 - String.Format( "lldm_zcinternal_panelsorder##" ).Length()":
	const const_availablePartLen = ( 254 - 35 );


	static ZCPanelsStorage Create( void ) {
		ZCPanelsStorage panel = new( 'ZCPanelsStorage' );

		panel.panelsorderChanged = true;
		panel.panelsResetRequest = true;
		panel.curpanels = new( 'ZCInfoPanelNodesKeeper' );
		panel.lastFontType = -1;

		return panel;
	}


	void UpdatePanels( bool fromCVar = false ) {
		uint warningsAmount = 0;
		panelsorderChanged = false;

		curPanels.Clear();

		String panelsOrderStr = ( fromCVar? GetPanelsorderFromCVars(): panelsstr );
		int curloglevel = CVar.FindCVar( "lldm_zc_loglevel" ).GetInt();

		if ( panelsOrderStr != "" ) {
			Array<String> neworder;

			if ( curloglevel >= LL_Debug )
				ZCGlobal.ClearscopeLog( LL_Debug, GetClassName() .. "::UpdatePanels(). panelsOrderStr: \"" .. TEXTCOLOR_OLIVE .. panelsOrderStr .. LLCOLOR_DEBUG .. "\" (" .. panelsOrderStr.Length() .. " chars)." );

			panelsOrderStr.Split( neworder, ",", TOK_SKIPEMPTY );

			int savedPanelsAmount = 0;

			// Restoring panels saved in the panels order CVar:
			for ( int i = 0; i < neworder.Size(); i++ ) {
    			class<Object> curclass = neworder[ i ];

				if ( curclass is "ZCBaseInfoPanelPart" ) {
					bool found = false;

					for ( int j = 0; j < defaultPanels.Size(); j++ ) {
						if ( defaultPanels[ j ].GetClass() == curclass ) {
							found = true;
							savedPanelsAmount++;
							curPanels.Push( defaultPanels[ j ] );

							if ( curloglevel >= LL_Debug )
								ZCGlobal.ClearscopeLog( LL_Debug, GetClassName() .. "::UpdatePanels(). Added Panel class \"" .. TEXTCOLOR_DARKGREEN .. neworder[ i ] .. LLCOLOR_DEBUG .. "\"." );

							break;
						}
    				}

					if ( !found && curloglevel >= LL_Developer )
						ZCGlobal.ClearscopeLog( LL_Developer, LLCOLOR_DARK .. GetClassName() .. "::UpdatePanels(). Class \"" .. LLCOLOR_NORMAL .. neworder[ i ] .. LLCOLOR_DARK .. "\" vanished from the *.cfg." );
    			} else if ( curclass ) {
					warningsAmount++;
					ZCGlobal.ClearscopeLog( LL_Main, LLCOLOR_DARK .. GetClassName() .. "::UpdatePanels(). Class \"" .. LLCOLOR_NORMAL .. neworder[ i ] .. LLCOLOR_DARK .. "\" isn't a Panel." );
				} else {
					ZCGlobal.ClearscopeLog( LL_Main, LLCOLOR_DARK .. GetClassName() .. "::UpdatePanels(). Cannot find Panel class \"" .. LLCOLOR_NORMAL .. neworder[ i ] .. LLCOLOR_DARK .. "\"" );
				}
			}

			// Primitive adding all of the brand-new-written infopanels:
			if ( !warningsAmount && savedPanelsAmount < defaultPanels.Size() ) {
				int defaultPanelsSize = defaultPanels.Size();
				int inserteePanelsAmountLeft = defaultPanelsSize - savedPanelsAmount;

				ZCGlobal.DataLog( LL_Debug, String.Format( "%s::UpdatePanels(). Was found only %i/%i infopanels from the default configuration file (*.cfg) in the saved panels order.", GetClassName(), savedPanelsAmount, defaultPanelsSize ) );

				// Comparing every panel from the "*.cfg" file with panels from current saved order:
				for ( int i = 0; i < defaultPanelsSize; i++ ) {
					class<ZCBaseInfoPanelPart> curDefpanelClass = ( ( i < defaultPanels.Size() && defaultPanels[ i ] )? defaultPanels[ i ].GetClass() : NULL );
					bool panelfound = false;

					// Checking if potential new panel is actually new:
					for ( int j = 0; j < curPanels.Size(); j++ ) {
						if ( curPanels.GetPanelClass( j ) == curDefpanelClass ) {
							panelfound = true;
							break;
						}
					}

					// Inserting new panel defined in "*.cfg" if it is not already in the current saved panels order:
					if ( !panelfound ) {
						ZCGlobal.DataLog( LL_Detailed, LLCOLOR_DARK .. GetClassName() .. "::UpdatePanels(). New infopanel class \"" .. TEXTCOLOR_DARKGREEN .. defaultPanels[ i ].GetClassName() .. LLCOLOR_DARK .. "\" saved at index " .. i .. "." );
						curPanels.Insert( i, defaultPanels[ i ] );

						if ( --inserteePanelsAmountLeft <= 0 )
							break;
					}
				} // of for ( int i = 0; i < defaultPanelsSize; i++ ) {}

			} // of if ( !warningsAmount && savedPanelsAmount < defaultPanels.Size() ) {}

		} // of if ( panelsOrderCVar.GetString() != "" ) {}


		// Reinitialization (if neccesary):
		if ( curPanels.Size() == 0 || warningsAmount > 0 ) {
			curPanels.Clear();

			if ( warningsAmount > 0 )
				ZCGlobal.ClearscopeLog( LL_Main, GetClassName() .. "::UpdatePanels(). It seems like data was corrupted (" .. TEXTCOLOR_FIRE .. warningsAmount .. " warnings\c-). Reinitialization..." );
			else if ( curloglevel >= LL_Debug )
				ZCGlobal.ClearscopeLog( LL_Developer, GetClassName() .. "::UpdatePanels(). Panels classes order not defined. First-launch initialization." );

			for ( int i = 0; i < defaultPanels.Size(); i++ )
				curPanels.Push( defaultPanels[ i ] );
		}

		panelsstr = "";

		// Unknown CVars references handling:
		for ( int i = 0; i < curPanels.Size(); i++ ) {
			ZCInfoPanelNode panelnode = curPanels.nodes[ i ];

			panelnode.InitCVar();

			if ( !panelnode.panelCVar )
				ZCGlobal.ClearscopeLog( LL_Emergency, GetClassName() .. "::UpdatePanels(). Panel \"" .. TEXTCOLOR_RED .. panelnode.panel.GetClassName() .. LLCOLOR_EMERGENCY .. "\" refers to the unknown CVar \"" .. TEXTCOLOR_RED .. panelnode.panel.optionCVarName .. LLCOLOR_EMERGENCY .."\"." );

			panelsstr = panelsstr .. panelnode.panel.GetClassName() .. ",";
		}

		ZCGlobal.DataLog( LL_Developer, GetClassName() .. "::UpdatePanels(). Amount of saved panels: " .. curPanels.Size() );

		InitSetPanelsorderCVars();
	} // of void UpdatePanels( bool fromCVar = false ) {}

	void InitSetPanelsorderCVars( String argorder = "" ) {
		newOrderStr = ( argorder == ""? panelsstr : argorder );
		newOrderSaveRequest = true;
	}

	// ZScript-ACS API cannot obtain more than one string with maximum of 255 characters.
	play void TickSetPanelsorderCVars( void ) {
		if ( newOrderSaveRequest ) {
			int orderStrLength = newOrderStr.Length();
			newOrderStrPartsAmount = int( ceil( orderStrLength / double( const_availablePartLen ) ) );

			for ( int i = 0; i < newOrderStrPartsAmount; i++ ) {
				int indexfrom = i * const_availablePartLen;
				int indexto = min( indexfrom + const_availablePartLen, orderStrLength );
				String curpart = newOrderStr.Mid( indexfrom, ( indexto - indexfrom ) );

				ZCGlobal.DataLog( LL_Debug, GetClassName() .. "::TickSetPanelsorderCVars(). Part " .. i .. "/" .. ( newOrderStrPartsAmount - 1 ) .. ": \"" .. TEXTCOLOR_GRAY .. curpart .. LLCOLOR_DEBUG .. "\"" );

				ZCGlobal.PrepareUserCVarString( "lldm_zcinternal_panelsorder_part" .. i, curpart );
				ACS_ExecuteAlways( -int( 'ZChecker_SetInfopanelPartCVar' ), 0, consoleplayer, i );
			}

			newOrderSaveRequest = false;
		}
	} // of void TickSetPanelsorderCVars( void ) {}


	static String GetPanelsorderFromCVars( void ) {
		String outstr = "";

		for ( int i = 0; i < 10; i++ ) {
			String curpart = CVar.GetCVar( "lldm_zcinternal_panelsorder_part" .. i, players[ consoleplayer ] ).GetString();

			if ( curpart.Length() == 0 )
				break;

			outstr = outstr .. curpart;
		}

		return outstr;
	} // of static String GetPanelsorderFromCVars( void ) {}

	void UpdateFont( void ) {
		int fonttype = -1;

		if ( !fonttypeCVar )
			fonttypeCVar = CVar.GetCVar( "lldm_zc_infopanel_fonttype", players[ consoleplayer ] );

		fonttype = fonttypeCVar.GetInt();

		if ( fonttype != lastFontType ) {
			bool updateLastFontType = true;
			Font newfont;
			String newfontName;

			switch ( fonttype ) {
				case 0:
					newfont = smallfont;
					break;
				case 1:
					newfont = Font.FindFont( "zchecker_smallfont_mono_shortspace" );
					break;
				case 2:
					newfont = Font.FindFont( "zchecker_smallfont_mono" );
					break;
				case 3:
					newfont = confont;
					break;
				case 4:
					newfontName = "newsmallfont";
					newfont = Font.FindFont( newfontName );
					break;
				case 5:
					newfontName = "newconsolefont";
					newfont = Font.FindFont( newfontName );
					break;
				default:
					updateLastFontType = false;
					ZCGlobal.DataLog( LL_Emergency, GetClassName() .. "::UpdateFont(). Unknown font type \"" .. fonttype .. "\"." );
					break;
			}

			if ( !newfont ) {
				ZCGlobal.DataLog( LL_Main, String.Format( ZCGlobal.Locz( "MAINMENUOPT_NOFONTMSG" ), newfontName ) );
				infopanelsFont = smallfont;
			} else {
				infopanelsFont = newfont;
			}

			infopanelsFontSpaceWidth = infopanelsFont.GetCharWidth( 32 );

			ZCGlobal.DataLog( LL_Debug, GetClassName() .. "::UpdateFont(). Font type changed to " .. fonttype .. ", space width is " .. infopanelsFontSpaceWidth .. "." );

			if ( updateLastFontType )
				lastFontType = fonttype;
			else
				fonttype = lastFontType;
		} // of if ( fonttype != lastFontType ) {}

	} // of void UpdateFont( void ) {}

} // of class ZCPanelsStorage {}



// Alternative strings API between ZScript and ACS. World-tick dependence 
//due to the ACSThinker.
class ZCZScriptACSStringsNode play {
	Array<String> strings;
}

class ZCZScriptACSStringsAPI: Thinker {
	ZCZScriptACSStringsNode storedTypes[ APIST_Quantity ];

	enum EStringType {
		APIST_Default = 0,
		APIST_Flags,
		APIST_States,

		APIST_Quantity
	};

	static ZCZScriptACSStringsAPI Get( void ) {
		ZCZScriptACSStringsAPI it = ZCZScriptACSStringsAPI( ThinkerIterator.Create( 'ZCZScriptACSStringsAPI', STAT_STATIC ).Next() );

		if ( !it ) {
			it = new( 'ZCZScriptACSStringsAPI' );
			it.ChangeStatNum( STAT_STATIC );

			for ( int itype = 0; itype < APIST_Quantity; itype++ )
				it.storedTypes[ itype ] = new( 'ZCZScriptACSStringsNode' );
		}

		return it;
	}

	String PopString( EStringType type ) {
		String outname = "";

		if ( type >= 0 && type < APIST_Quantity ) {
			let curstorage = storedTypes[ type ];
			int storedStringsSize = curstorage.strings.Size();

			if ( storedStringsSize ) {
				outname = curstorage.strings[ storedStringsSize - 1 ];
				curstorage.strings.Pop();
			}
		}

		return outname;
	} // of String PopString( EStringType type ) {}

	void AddString( EStringType type, String str ) {
		storedTypes[ type ].strings.Push( str );
	}

	static String PopStringStatic( EStringType type ) {
		return Get().PopString( type );
	}

	static void AddStringStatic( EStringType type, String str ) {
		Get().AddString( type, str );
	}
} // of class ZCZScriptACSStringsAPI: Thinker {}



// Elementary compatible dictionary cell.
class ZCInternalStringDictionaryCell {
	String key;
	String value;

	static ZCInternalStringDictionaryCell Create( String initkey, String initval ) {
		ZCInternalStringDictionaryCell newcell = new( "ZCInternalStringDictionaryCell" );

		newcell.key = initkey;
		newcell.value = initval;

		return newcell;
	}
}

// Storage cell of the actors names slightly speeds up the names search 
//process. No binary tree or hashmap used because name masks/wildcards may be 
//provided, absolutely removing all pros from specified data structures.
class ZCInternalActorNameCell {
	const ARRAY_Quantity = 28;

	int firstchar; // "0" is '_', "1" is 'a', "26" is 'z', "27" is other char (if any...).
	Array<String> names;		// Lowercased;
	Array<String> orignames; 	// Original.

	static ZCInternalActorNameCell Create( int initFirstchar ) {
		ZCInternalActorNameCell newcell = new( 'ZCInternalActorNameCell' );
		newcell.firstchar = initFirstchar;

		return newcell;
	}

	// First character. "0" is '_', "1" is 'a', "26" is 'z' and "27" is other char (if any).
	static clearscope int GetClassnameCellIndex( String clsname ) {
		int outIndex = 27;

		if ( clsname != "" ) {
			int firstchar = clsname.CharCodeAt( 0 );

			// Converting to uppercase:
			if ( firstchar >= CHAR_LETTER_A_LOWER && firstchar <= CHAR_LETTER_Z_LOWER )
				firstchar &= ~0x20;

			if ( firstchar == CHAR_UNDERSCORE )
				outIndex = 0; // '_'.
			else if ( firstchar >= CHAR_LETTER_A_UPPER && firstchar <= CHAR_LETTER_Z_UPPER )
				outIndex = firstchar - CHAR_LETTER_A_UPPER + 1; // 'A'..'Z'.
		}

		return outIndex;
	}
} // of class ZCInternalActorNameCell {}

struct ZCheckerHandlerDatascopeVariables {
	transient CVar infopanelStartXCVar, infopanelStartYCVar;
	transient CVar infopanelScaleCVar;
	transient CVar infopanelOffsetXCVar, infopanelPartsSpaceYCVar;

	transient CVar infopanelDimEnabledCVar;
	transient CVar infopanelDimColorCVar;
	transient CVar infopanelDimAlphaCVar;
	transient CVar infopanelDimBorderWidthCVar;

	int lastLocalizationTestGameTic;
	String lastLocalizedTestString;

} // of struct ZCheckerHandlerDatascopeVariables {}

