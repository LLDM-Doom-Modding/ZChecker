//

class ZCOptionMenuPanels: OptionMenu {
	enum EPanelMenuActionTypes {
		PMAT_None			= -1,
		PMAT_EnterSubmenu	= 0,
		PMAT_MoveItemUp,
		PMAT_MoveItemDown,

		PMAT_Quantity
	};

	transient CVar panelsorder;
	transient CVar firstPanelItemPosCVar, lastPanelItemPosCVar;
	int firstPanelItemPos, lastPanelItemPos;


	void SetPanelOptionsOrder( OptionMenuDescriptor desc ) {
		String curorder = "";

		for ( int i = firstPanelItemPos; i < lastPanelItemPos; i++ )
			curorder = curorder .. desc.mItems[ i ].GetClassName() .. ",";

		panelsorder.SetString( curorder );
	}

	void GetPanelOptionsOrder( OptionMenuDescriptor desc ) {
		Array<String> neworder;

		panelsorder.GetString().Split( neworder, ",", TOK_SKIPEMPTY );

		for ( int i = 0; i < neworder.Size(); i++ ) {
			class<ZCBaseInfoPanelPart> curclass = neworder[ i ];

			if ( curclass is "ZCBaseInfoPanelPart" ) {
				ZCGlobal.ClearscopeLog( LL_Main, GetClassName() .. "::GetPanelOptionsOrder(). " .. TEXTCOLOR_GREEN .. "Added Panel class \"" .. neworder[ i ] .. "\"." );
			} else {
				ZCGlobal.UILog( LL_Detailed, GetClassName() .. "::GetPanelOptionsOrder(). Cannot find Panel class \"" .. neworder[ i ] .. "\"." );
			}
		}
	}

	override void Init( Menu parent, OptionMenuDescriptor desc ) {
		ZCheckerHandler handler = ZCheckerHandler( StaticEventHandler.Find( "ZCheckerHandler" ) );

		firstPanelItemPosCVar = CVar.GetCVar( "mcm_zcheckerinternal_ui_firstinfopanelpos", players[ consoleplayer ] );
		lastPanelItemPosCVar = CVar.GetCVar( "mcm_zcheckerinternal_ui_lastinfopanelpos", players[ consoleplayer ] );

		if ( desc.mItems.Size() < 20 ) {
			// Size limit is a random constant satisfying the requirements, in fact...

			firstPanelItemPos = 4;
			lastPanelItemPos = 4;

			if ( !handler ) {
				ZCGlobal.ClearscopeLog( LL_Emergency, GetClassName() .. "::Init(). Cannot locate ZChecker static event handler." );
				Super.Init( parent, desc );
				return;
			}

			uint itemsOffset = firstPanelItemPos;

			for ( int i = 0; i < handler.zcInfoPanelClasses.Size(); i++ ) {
				ZCBaseInfoPanelPart panel = handler.zcInfoPanelClasses[ i ];

				ZCOptionMenuItemInfopanel item = new( "ZCOptionMenuItemInfopanel" );
				ZCOptionMenuItemInfopanelSubmenu submenuItem = NULL;

				if ( panel.optionSubmenuName ) {
					submenuItem = new( "ZCOptionMenuItemInfopanelSubmenu" );
					submenuItem.Init( item, panel );
				}

				item.Init( panel.optionLabel, panel.optionCVarName, panel.optionSubmenuName );
				desc.mItems.Insert( i + itemsOffset, item );
				lastPanelItemPos++;

				if ( panel.optionSubmenuName ) {
					itemsOffset++;
					lastPanelItemPos++;
					desc.mItems.Insert( i + itemsOffset, submenuItem );
				}
			} // of for ( int i = 0; i < handler.zcInfoPanelClasses.Size(); i++ ) {}

			firstPanelItemPosCVar.SetInt( firstPanelItemPos );
			lastPanelItemPosCVar.SetInt( lastPanelItemPos );
		} else {
			firstPanelItemPos = firstPanelItemPosCVar.GetInt();
			lastPanelItemPos = lastPanelItemPosCVar.GetInt();
		}

		Super.Init( parent, desc );
	} // of override void Init( Menu parent, OptionMenuDescriptor desc ) {}

	override bool OnUIEvent( UIEvent e ) {
		int curpos = mDesc.mSelectedItem;
		ZCOptionMenuItemInfopanel curitem = NULL;
		if ( curpos > 0 )
			curitem = ZCOptionMenuItemInfopanel( mDesc.mItems[ curpos ] );

		//console.printf( "e.Type: " .. e.Type .. ", e.KeyChar: " .. e.KeyChar .. ", e.KeyString: \"" .. e.KeyString .. "\" (len " .. e.KeyString.Length() .. ")" );

		EPanelMenuActionTypes actiontype = PMAT_None;
		int key = e.KeyChar;

		if ( e.Type == e.Type_KeyDown ) {
			if ( key == e.Key_F3 || key == e.Key_F4 || key == e.Key_Tab )
				actiontype = PMAT_EnterSubmenu;

		} else if ( e.Type == e.Type_Char ) {
			if ( key == 0x45 || key == 0x65 || key == 0x20 )
				actiontype = PMAT_EnterSubmenu;
			else if ( key == 0x2D )
				actiontype = PMAT_MoveItemUp;
			else if ( key == 0x2B )
				actiontype = PMAT_MoveItemDown;
		} else if ( e.Type == e.Type_WheelUp ) {
			actiontype = PMAT_MoveItemUp;
		} else if ( e.Type == e.Type_WheelDown ) {
			actiontype = PMAT_MoveItemDown;
		}

		if ( curitem && actiontype != PMAT_None && curpos < lastPanelItemPos && curpos >= firstPanelItemPos ) {
			int moveOffset = ( curitem.mSubmenu? 2 : 1 );

			if ( actiontype == PMAT_MoveItemUp || actiontype == PMAT_MoveItemDown ) {
				bool movePrepared = false;

				if ( actiontype == PMAT_MoveItemUp ) {
					int newpos = curpos - ( 1 + !!( mDesc.mItems[ curpos - 1 ] is "ZCOptionMenuItemInfopanelSubmenu" ) );

					curitem = ZCOptionMenuItemInfopanel( mDesc.mItems[ newpos ] );
					moveOffset = ( curitem && curitem.mSubmenu? 2 : 1 );

					movePrepared = ( curpos - moveOffset >= firstPanelItemPos );

					curpos = newpos;
				} else {
					movePrepared = ( curpos + moveOffset < lastPanelItemPos );
				}

				if ( movePrepared ) {
					OptionMenuItem movingitem = NULL;

					int curMoveOffset = moveOffset;

					if ( ZCOptionMenuItemInfopanel( mDesc.mItems[ curpos + moveOffset ] ).mSubmenu )
						moveOffset++;

					while ( curMoveOffset --> 0 ) {
						movingitem = mDesc.mItems[ curpos ];
						mDesc.mItems.Delete( curpos, 1 );
						mDesc.mItems.Insert( curpos + moveOffset, movingitem );
					}

					Menu.GetCurrentMenu().MenuEvent( ( actiontype == PMAT_MoveItemUp? MKEY_Up : MKEY_Down ), false );
					Menu.MenuSound( "menu/change" );
				}

			} else if ( curitem.mSubmenu && ( actiontype == PMAT_EnterSubmenu ) ) {
				// Tab, F4, 'e', 'E' and space opens a submenu (if any).
				Menu.MenuSound( "menu/choose" );
				Menu.SetMenu( curitem.mSubmenu );
				return true;
			}
		} // of if ( curitem && actiontype != PMAT_None && curpos < lastPanelItemPos && curpos >= firstPanelItemPos ) {}

		return Super.OnUIEvent( e );
	} // of override bool OnUIEvent( UIEvent e ) {}

} // of class ZCOptionMenuPanels: OptionMenu {}


class ZCOptionMenuItemInfopanel: OptionMenuItemOption {
	Name mSubmenu;

	ZCOptionMenuItemInfopanel Init( String label, Name command, Name submenuName ) {
		Super.Init( label, command, 'OnOff' );
		mSubmenu = submenuName;

		return self;
	}
}

class ZCOptionMenuItemInfopanelSubmenu: OptionMenuItemStaticText {
	ZCOptionMenuItemInfopanel masterMenuItem;
	ZCBaseInfoPanelPart masterInfoPanel;
	ZCOptionMenuPanels masterMenu;

	OptionMenuDescriptor desc;

	ZCOptionMenuItemInfopanelSubmenu Init( ZCOptionMenuItemInfopanel masteritem, ZCBaseInfoPanelPart infopanel ) {
		masterMenuItem = masteritem;
		masterInfoPanel = infopanel;
		mLabel = "";

		masterMenu = ZCOptionMenuPanels( Menu.GetCurrentMenu() );
		desc = OptionMenuDescriptor( MenuDescriptor.GetDescriptor( infopanel.optionSubmenuName ) );

		return self;
	}

	override int Draw( OptionMenuDescriptor desc, int y, int indent, bool selected ) {
		bool masterItemSelected = false;

		if ( desc && desc.mSelectedItem > 0 ) {
			mLabel = masterInfoPanel.GetSubmenuLabel( masterMenuItem, desc );
			masterItemSelected = ( desc.mItems[ desc.mSelectedItem ] == masterMenuItem );
		}

		drawLabel( indent, y, masterItemSelected? OptionMenuSettings.mFontColorSelection : Font.CR_DARKGRAY );

		return indent;
	}

	/*override bool MouseEvent( int type, int x, int y ) {
		if ( type == Menu.MOUSE_Click && masterMenu ) {
			for ( int i = masterMenu.firstInfoPanelPos; i < masterMenu.lastInfoPanelPos; i++ ) {
				if ( desc.mItems[ i ] == masterMenuItem ) {
					desc.mSelectedItem = i;
					break;
				}
			}

			return Menu.GetCurrentMenu().MenuEvent( Menu.MKEY_Enter, true );
		}

		return true;
	}*/
}
