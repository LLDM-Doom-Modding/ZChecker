//
// Infopanels for all global large systems related to- and created for Actors.
//
// In particular:
//   1. Inventory list;
//   2. States machine;
//   3. Visual stuff.
//


class ZCInventoryInfoPanel: ZCBaseInfoPanelPart {
	ZCInventoryQuicksort qsort;
	int sortPrevTypeOption;

	//Array<ZCInvFieldData> prevInvs;


	override void OnCreate( void ) {
		optionCVarName = "lldm_zcpanel_inventory";
		optionLabel = "$ZCHECKER_INFOPANELS_INVENTORY";
		optionLabelExtraHotkeys = "$ZCHECKER_INFOPANELKEYS_INVENTORY";
		optionSubmenuName = "ZChecker_InfopanelSubmenu_inventory";

		sortPrevTypeOption = -1;

		//for ( int i = 0; i < MaxTestee; i++ )
		//	prevInvs.Push( new( 'ZCInvFieldData' ) );
	}

	override String GetInfo( Actor thing, ZCheckerField thingfield ) {
		if ( thing is "Inventory" && Inventory( thing ).owner )
			return DARKGRAY .. "Inventory: (owned).";

		String outstr = DARKGRAY .. "Inventory\c-: " .. GRAY;
		Inventory item = thing.Inv;

		if ( !item )
			return outstr .. "none.";

		// [McM] Tracking of the inventory amount changing not finished and 
		//therefore not available in the ZChecker beta-versions. The color 
		//interpretation of obtaining/losing inventory isn't ready yet...


		uint cellsAmountOption = CVar.GetCVar( "lldm_zcpanel_var_invamount", players[ consoleplayer ] ).GetInt();
		uint sortTypeOption = CVar.GetCVar( "lldm_zcpanel_var_invsorttype", players[ consoleplayer ] ).GetInt();
		bool sortReverseOption = CVar.GetCVar( "lldm_zcpanel_var_invsortascend", players[ consoleplayer ] ).GetBool();

		// Reiniting a quicksort keeper:
		if ( !qsort || ( sortPrevTypeOption != sortTypeOption && sortTypeOption != ZCInventoryQuicksort.QST_None ) ) {
			switch ( sortTypeOption ) {
				case ZCInventoryQuicksort.QST_Amount:
					qsort = new( 'ZCInventoryAmountQuicksort' );
					break;
				case ZCInventoryQuicksort.QST_MaxAmount:
					qsort = new( 'ZCInventoryMaxAmountQuicksort' );
					break;
				case ZCInventoryQuicksort.QST_Names:
				default:
					qsort = new( 'ZCInventoryQuicksort' );
					break;
			}
		}

		// Old versions of the engine has some troubles with the correct 
		//arrays initialization.
		qsort.arr.Clear();

		while ( item ) {
			qsort.arr.Push( item );
			item = item.Inv;
		}

		// Applying a sorting:
		if ( sortTypeOption != ZCInventoryQuicksort.QST_None )
			qsort.Sort();

		if ( sortReverseOption )
			qsort.Reverse();

		// Output generating:
		int qsortArraySize = qsort.arr.Size();

		for ( int i = 0; i < min( qsortArraySize, cellsAmountOption ); i++ ) {
			item = qsort.arr[ i ];
			int curAmount = item.Amount;
			int maxAmount = item.MaxAmount;

			String elemstr = "\n* ";

			if ( curAmount > 0 ) {
				String amountColor = ( curAmount == maxAmount? DARKGREEN : DARKGRAY );

				elemstr = amountColor .. elemstr .. GRAY .. item.GetClassName();

				if ( curAmount != 1 )
					elemstr = elemstr .. amountColor .. " x" .. curAmount;
			} else {
				elemstr = BLACK .. elemstr .. item.GetClassName() .. " x0";
			}

			outstr = outstr .. elemstr;
		} // of for ( int i = 0; i < min( qsortArraySize, cellsAmountOption ); i++ ) {}

		if ( qsortArraySize > cellsAmountOption )
			outstr = outstr .. TEXTCOLOR_DARKGRAY .. "\n  (..." .. ( qsortArraySize - cellsAmountOption ) .. " more)";

		return outstr;
	} // of override String GetInfo( Actor thing, ZCheckerField thingfield ) {}


	override String GetSubmenuLabel( OptionMenuDescriptor desc ) {
		String outlabel;

		ResetSubmenuItemsIterator();

		int itemsAmount = GetNextSubmenuItemIntSelection( desc );

		OptionMenuItem sortmodeMenuItem = GetNextSubmenuItem( desc );
		int itemsSortMode = GetSubmenuItemIntSelection( sortmodeMenuItem );
		String itemsSortModeStr = GetSubmenuItemStrSelection( sortmodeMenuItem );

		if ( itemsSortMode > 0 )
			outlabel = String.Format( "%i%s %s", itemsAmount, ZCGlobal.Locz( "PANELSUBLABEL_INV_SORTEDBY" ), itemsSortModeStr );
		else
			outlabel = String.Format( "%i%s", itemsAmount, ZCGlobal.Locz( "PANELSUBLABEL_INV_UNSORTED" ) );

		if ( !!GetNextSubmenuItemIntSelection( desc ) )
			outlabel = outlabel .. " " .. ZCGlobal.Locz( "PANELSUBLABEL_INV_REVERSED" );

		return ZCGlobal.Locz( "PANELSUBMENU_DEFAULTLABEL" ) .. ZCGlobal.Locz( "PANELSUBLABEL_INV_PREFIX" ) .. outlabel;
	} // of override String GetSubmenuLabel( OptionMenuDescriptor desc ) {}

} // of class ZCInventoryInfoPanel: ZCBaseInfoPanelPart {}

/*
// [McM] Tracking of the inventory amount changing not finished and therefore 
//not available in the ZChecker beta-versions.

class ZCInvFieldNode {
	ZCInvFieldNode next, prev;

	Inventory inv;
	int visualTicks;		// Positive: green (inv. obtained); negative: red (inv. taken).
}

class ZCInvFieldData {
	ZCInvFieldNode head, tail;
	ZCInvFieldNode firstvisible;

	ZCInvFieldNode AddNode( Inventory inv ) {
		ZCInvFieldNode newnode = new( 'ZCInvFieldNode' );

		if ( !head ) {
			head = tail = newnode;
		} else {
			tail.next = newnode;
			newnode.prev = tail;
			tail = newnode;
		}

		newnode.inv = inv;
		newnode.visualTicks = 5;

		return newnode;
	}

	void UpdateByActorInventory( Actor owner ) {
		Inventory aitem = owner.inv;
		Inventory sitem = head.inv;

		while ( aitem ) {
			if ( aitem != sitem ) {
				// <...>
			}

			aitem = aitem.inv;
		}
	}
} // of class ZCInvFieldData {}*/



class ZCStatesInfoData {
	State st;

	SpriteID lastsprite;
	int lastframe;
}

class ZCStatesInfoPanel: ZCBaseInfoPanelPart {
	Array<ZCStatesInfoData> prevLogStates;
	int prevLogStateTics;

	String tempFlagsStr;

	transient CVar statesAmountCVar, statesLoggingCVar; 				// For states in general;
	transient CVar stateTicsCVar, stateFlagsCVar, stateExtraInfoCVar; 	// For current state.
	transient CVar pspriteOverlaysModeCVar, pspriteExtraOverlayIDCVar;	// For PSprite layers/overlays.
	protected int curstateFlagsType, curstateExtraInfo;					// Necessary for the "::addPSpriteState()".

	// "Unexpected JIT error: Unknown REGT value passed to EmitPARAM" raises when the ".Left()" method calls for the const Strings.
	String const_TicsStringVisualLeft, const_TicsStringVisualPassed, const_StatesEmptyLines;

	const const_SameSprite = 2;


	override void OnCreate( void ) {
		const_TicsStringVisualLeft = "====================";
		const_TicsStringVisualPassed = "++++++++++++++++++++";
		const_StatesEmptyLines = "\n\n\n\n\n\n\n\n\n\n"; // With total of 160 newlines.
		const_StatesEmptyLines = const_StatesEmptyLines .. const_StatesEmptyLines .. const_StatesEmptyLines .. const_StatesEmptyLines;
		const_StatesEmptyLines = const_StatesEmptyLines .. const_StatesEmptyLines .. const_StatesEmptyLines .. const_StatesEmptyLines;

		optionCVarName = "lldm_zcpanel_states";
		optionLabel = "$ZCHECKER_INFOPANELS_STATES";
		optionLabelExtraHotkeys = "$ZCHECKER_INFOPANELKEYS_STATES";
		optionSubmenuName = "ZChecker_InfopanelSubmenu_states";

		for ( int i = 0; i < MaxTestee; i++ )
			prevLogStates.Push( new( 'ZCStatesInfoData' ) );
	}


	protected String UndecoratedState( State st ) {
		return String.Format(
				SpriteID( st.sprite ) .. " %c %i", ( st.bSameFrame? 0x23 : st.frame + CHAR_LETTER_A_UPPER ), st.tics
			);
	}

	protected String DecoratedState( State st, SpriteID prevcopysprite = -1, int prevcopyframe = -1 ) {
		// Note that function is optimized and thus can be unreadable.

		// SpriteID: 0 == "TNT1A0", 1 == fixed, 2 == SameSprite.
		SpriteID sprite = SpriteID( st.sprite );

		if ( sprite == 0 )
			return String.Format( "%sTNT1 A %i\c-", GRAY, st.tics );

		String spritecolor;

		if ( sprite != const_SameSprite ) {
			spritecolor = SAPPHIRE;
		} else {
			spritecolor = DARKGREEN; // Same sprite.

			if ( prevcopysprite != -1 )
				sprite = prevcopysprite;
		}

		String framecolor = ( st.bSameFrame? GREEN : LIGHTBLUE );  // Same frame.
		//String ticscolor = ( st.tics == 0? DARKGRAY : SAPPHIRE ); // Zero-delay tic.

		//console.printf( sprite .. "/frame %i/tics %i", ( st.bSameFrame? ( prevcopyframe != -1? prevcopyframe + 65 : 0x2A ) : st.Frame + 65 ), st.tics );

		return String.Format(
			spritecolor .. sprite .. "%s %c %s%i\c-",
			framecolor, ( st.bSameFrame? ( prevcopyframe != -1? prevcopyframe + CHAR_LETTER_A_UPPER : 0x23 ) : st.frame + CHAR_LETTER_A_UPPER ),
			SAPPHIRE, st.tics
		);
	} // of protected String DecoratedState( State st, State prevst = NULL ) {}


	protected void DoStatesLogging( Actor thing, ZCheckerField thingfield ) {
		State curState = thing.CurState;
		State st = curState;
		SpriteID prevcopysprite = thing.sprite;
		int prevcopyframe = thing.frame;

		if ( thingfield.actorType == ZCheckerField.IAT_StateProvider ) {
			Actor weapOwner = getItemPlayerOwner( Inventory( thing ) );

			if ( weapOwner && weapOwner.player ) {
				PSprite pspr = weapOwner.player.psprites;
				curState = st = pspr.CurState;
				prevcopysprite = pspr.sprite;
				prevcopyframe = pspr.frame;
			}
		}

		int fieldPos = thingfield.GetFieldPos();
		State prevlogst = prevLogStates[ fieldPos ].st;

		if ( prevlogst && prevlogst != st ) {
			String logstr;

			/*String logstr = String.Format( "%i. \c-" .. SAPPHIRE .. "%s " .. LIGHTBLUE .. "%c " .. SAPPHIRE .. "%i\c-",
							( fieldPos + 1 ),
							( isWeaponInHands? "" .. weapOwner.player.psprites.sprite : "" .. thing.Sprite ),
							st.Frame + 65, st.Tics );*/

			State nextlogst = prevlogst.NextState;
			bool zeroSkipped = ( nextlogst && nextlogst.tics == 0 );

			while ( nextlogst && nextlogst.tics == 0 )
				nextlogst = nextlogst.NextState;

			bool stateForced = ( nextlogst != st );
			bool sameStateFrame = ( ( st.sprite == const_SameSprite ) || st.bSameFrame );

			if ( zeroSkipped || stateForced || sameStateFrame ) {
				if ( sameStateFrame ) {
					logstr.AppendFormat( DARKGRAY .. "real \"%s%s\"", UndecoratedState( st ), DARKGRAY );
				}

				if ( zeroSkipped ) {
					if ( logstr.Length() > 0 )
						logstr = logstr .. "; ";

					logstr = logstr .. DARKGRAY .. "zero states";
				}

				if ( stateForced ) {
					State prevnextst = prevlogst;

					if ( prevlogst.Tics == prevLogStateTics && prevlogst.NextState )
						prevnextst = prevlogst.NextState;

					if ( logstr.Length() > 0 )
						logstr = logstr .. "; ";

					logstr.AppendFormat(
						"had to be \"%s%s\", forced at tic %i",
						DecoratedState( prevnextst ), DARKGRAY, prevLogStateTics
					);
				} // of if ( stateForced ) {}

				logstr = DARKGRAY .. " (" .. logstr .. ")";
			} // of if ( zeroSkipped || stateForced || sameStateFrame ) {}

			// Print to console:
			logstr = ( fieldPos + 1 ) .. ". " .. DecoratedState( st, prevcopysprite, prevcopyframe ) .. logstr;

			ZCGlobal.ClearscopeLog( LL_Main | LL_NoDot, logstr );

			// Save previous sprite/frame:
			if ( st.sprite != const_SameSprite )
				prevcopysprite = st.sprite;

			if ( !st.bSameFrame )
				prevcopyframe = st.frame;

		} // of if ( prevlogst && prevlogst != st ) {}

		prevLogStates[ fieldPos ].st = st;
		prevLogStateTics = thing.Tics;
	} // of protected void DoStatesLogging( Actor thing, ZCheckerField thingfield ) {}

	protected void LogRealtimeSpriteFrameChange( Actor thing, ZCheckerField thingfield ) {
		// Function is not used right now.
		int fieldPos = thingfield.GetFieldPos();

		ZCGlobal.ClearscopeLog( LL_Main | LL_NoDot, String.Format(
				"%i. Sprite/frame changed to \"" .. TEXTCOLOR_RED .. "%s %c\c-\" from \"%s %c\" at tic %i.",
				fieldPos + 1,
				"" .. SpriteID( thing.sprite ), thing.frame + CHAR_LETTER_A_UPPER,
				"" .. SpriteID( prevLogStates[ fieldPos ].lastsprite ), prevLogStates[ fieldPos ].lastframe + CHAR_LETTER_A_UPPER,
				thing.tics
			) );
	}


	protected void addFlag( bool flagcond, String flagname ) {
		tempFlagsStr = tempFlagsStr .. ( flagcond? GREEN : BLACK ) .. flagname .. " ";
	}

	protected String addPSpriteState( PSprite pspr ) {
		if ( !pspr || !pspr.CurState )
			return "";

		State st = pspr.CurState;
		String addstr = "";
		tempFlagsStr = "";

		if ( curstateFlagsType ) {
			switch ( curstateFlagsType ) {
				case 1:
					addFlag( st.bFast, "fst" );
					addFlag( st.bSlow, "slw" );
					addFlag( st.bFullbright, "lit" );
					addFlag( st.bSameFrame, "frcp" );
					addFlag( st.bDehacked, "deh" );
					addFlag( pspr.bCVarFast, "cvrf" );
					addFlag( pspr.processPending, "pend" );
					addFlag( pspr.firstTic, "1st" );
					addFlag( pspr.bAddWeapon, "+wp" );
					addFlag( pspr.bAddBob, "+bob" );
					addFlag( pspr.bPowDouble, "x2sp" );
					break;
				case 2:
					addFlag( st.bFast, "fast" );
					addFlag( st.bSlow, "slow" );
					addFlag( st.bFullbright, "lit" );
					addFlag( st.bSameFrame, "sameframe" );
					addFlag( st.bDehacked, "deh-modified" );
					addFlag( pspr.bCVarFast, "cvarfast" );
					addFlag( pspr.processPending, "pending" );
					addFlag( pspr.firstTic, "1sttic" );
					addFlag( pspr.bAddWeapon, "addweap" );
					addFlag( pspr.bAddBob, "addbob" );
					addFlag( pspr.bPowDouble, "x2speed" );
					break;
				default:
					break;
			} // of switch ( curstateFlagsType ) {}

			addstr = "\n" .. BreakPanelLines( tempFlagsStr, textwidth: 28 );
		} // of if ( curstateFlagsType ) {}

		if ( curstateExtraInfo ) {
			addstr.AppendFormat( DARKRED .. "\n CurPos: (%.3f, %.3f);\n OldPos: (%.3f, %.3f);\n Alpha: " .. BRICK .. " %.3f" .. DARKRED .. ";\n Layer ID: " .. BRICK .. " %i" .. DARKRED .. ".",
							pspr.x, pspr.y, pspr.oldx, pspr.oldy,
							pspr.alpha, pspr.id );

			addstr = addstr .. DARKGRAY .. "\n TicRange " .. GRAY .. st.TicRange .. DARKGRAY .. ", misc " .. GRAY .. st.misc1 .. DARKGRAY .. "/" .. GRAY .. st.misc2;
		} // of if ( curstateExtraInfo ) {}

		return addstr;
	} // of protected String addPSpriteState( PSprite pspr, String psprName ) {}



	override String GetInfo( Actor thing, ZCheckerField thingfield ) {
		String outStr = "";
		State curState = thing.CurState;
		State st = curState;

		bool isWeaponInHands = ( thingfield.actorType == ZCheckerField.IAT_StateProvider );
		PlayerPawn weapOwner = NULL;

		Array<State> prevstates;
		Array<String> prevstatesstr;

		if ( !statesAmountCVar ) {
			statesAmountCVar = CVar.GetCVar( "lldm_zcpanel_var_statesamount", players[ consoleplayer ] );
			statesLoggingCVar = CVar.GetCVar( "lldm_zcpanel_var_stateslogging", players[ consoleplayer ] );
			stateTicsCVar = CVar.GetCVar( "lldm_zcpanel_var_statetic", players[ consoleplayer ] );
			stateFlagsCVar = CVar.GetCVar( "lldm_zcpanel_var_stateflags", players[ consoleplayer ] );
			stateExtraInfoCVar = CVar.GetCVar( "lldm_zcpanel_var_stateextras", players[ consoleplayer ] );
			pspriteOverlaysModeCVar = CVar.GetCVar( "lldm_zcpanel_var_statepsprlayersmode", players[ consoleplayer ] );
			pspriteExtraOverlayIDCVar = CVar.GetCVar( "lldm_zcpanel_var_statepsprlayerid", players[ consoleplayer ] );
		}

		uint statesAmount = statesAmountCVar.GetInt();
		bool statesLogging = statesLoggingCVar.GetBool();

		if ( isWeaponInHands ) {
			weapOwner = getItemPlayerOwner( Inventory( thing ) );

			if ( weapOwner.player.psprites )
				curState = st = weapOwner.player.psprites.CurState;
		}

		if ( statesAmount <= 0 )
			statesAmount = 0x1FFF;

		// States and loops information:
		prevstates.Clear();
		prevstatesstr.Clear();
		outStr = outStr .. ( isWeaponInHands? "PSprite states" : "States" );

		if ( statesLogging ) {
			// Do logging:
			outStr = outStr .. DARKGRAY .. " [" .. LIGHTBLUE .. "logging" .. DARKGRAY .. "]";
			DoStatesLogging( thing, thingfield );

		} else if ( prevLogStates[ 0 ].st ) {
			// Clean the logging cache:
			for ( int i = 0; i < MaxTestee; i++ )
				prevLogStates[ i ].st = NULL;
		}

		outStr = outStr .. NORMAL .. ":\n";

		bool loopFound = false;
		int statesCnt = 0;
		SpriteID prevcopysprite = thing.sprite;
		int prevcopyframe = thing.frame;

		do {
			prevstates.Push( st );
			prevstatesstr.Push( DecoratedState( st, prevcopysprite, prevcopyframe ) );

			st = st.NextState;

			int backloopStateIndex = prevstates.Find( st );

			if ( loopFound = ( backloopStateIndex != prevstates.Size() ) ) {
				// Difference the one-state loop (waiting) and the normal loop:
				if ( backloopStateIndex == statesCnt ) {
					prevstatesstr[ statesCnt ] = prevstatesstr[ statesCnt ] .. TEXTCOLOR_WHITE .. " (wait)";
				} else {
					prevstatesstr[ backloopStateIndex ] = prevstatesstr[ backloopStateIndex ] .. TEXTCOLOR_WHITE .. " <<loop";
					prevstatesstr[ statesCnt ] = prevstatesstr[ statesCnt ] .. TEXTCOLOR_WHITE .. " ^ loop";
				}
			}

			if ( st ) {
				if ( st.sprite != 2 )
					prevcopysprite = st.sprite;

				if ( !st.bSameFrame )
					prevcopyframe = st.frame;
			}

			statesCnt++;
		} while ( st && st != curState && statesCnt < statesAmount && !loopFound );

		for ( int i = 0; i < prevstatesstr.Size(); i++ )
			outStr.AppendFormat( "%s\n", prevstatesstr[ i ] );

		RemoveLastNewlineSymbol( outStr );

		if ( statesAmount < 0x1FFF && statesCnt < statesAmount )
			outStr.AppendFormat( const_StatesEmptyLines.Left( statesAmount - statesCnt - 1 ) );


		st = curState;
		PSprite pspr = NULL;

		if ( weapOwner && weapOwner.player )
			pspr = weapOwner.player.psprites;

		// Tics information:
		if ( stateTicsCVar.GetBool() ) {
			outStr = outStr .. "\n";

			int sttics = st.tics;

			if ( sttics == -1 ) {
				outStr = outStr .. DARKGREEN .. "===== static =====";

			} else if ( sttics > 0 ) {
				int ticsbarLeft = 1;
				int curtics = ( isWeaponInHands? weapOwner.player.psprites.Tics : thing.tics );

				if ( curtics > 1 )
					ticsbarLeft = int( 20 * ( curtics - 1 ) / ( sttics != 0? sttics : 1 ) );

				if ( ticsbarLeft > 20 ) {
					// Tics is set to the values greater than a state limit during state delay.
					outStr = outStr .. LIGHTBLUE .. "<<<" .. GRAY;

				} else {
					// Form a string like "===>++++++++++++".
					outStr.AppendFormat( "%s%s>%s%s%s",
						DARKGREEN,
						const_TicsStringVisualPassed.Left( 20 - ticsbarLeft ), BLACK,
						const_TicsStringVisualLeft.Left( ticsbarLeft - 1 ), DARKGRAY
					);

					/*String ticbar = DARKGREEN;

					for ( int i = 0; i < 20 - ticsbarLeft; i++ )
						ticbar = ticbar .. "=";

					ticbar = ticbar .. ;

					for ( int i = 0; i < ticsbarLeft - 1; i++ )
						ticbar = ticbar .. "+";*/

					//outStr = outStr .. ticbar .. DARKGRAY;
				}

			} else {
				// When some state is set to "-2" and below.
				outStr = outStr .. DARKGREEN .. "== pseudostatic ==";
			}


			if ( !isWeaponInHands )
				outStr = outStr .. " (" .. thing.tics .. ")";
			else if ( pspr )
				outStr = outStr .. " (" .. pspr.tics .. ")";
		}

		// Extra state information:
		curstateExtraInfo = stateExtraInfoCVar.GetBool();
		curstateFlagsType = stateFlagsCVar.GetInt();

		if ( !curstateExtraInfo && !curstateFlagsType )
			return outStr; // Nothing more to print.


		if ( !isWeaponInHands ) {
			tempFlagsStr = "";

			if ( curstateFlagsType ) {
				switch ( curstateFlagsType ) {
					case 1:
						addFlag( st.bFast, "fst" );
						addFlag( st.bSlow, "slw" );
						addFlag( st.bFullbright, "lit" );
						addFlag( st.bNoDelay, "-dly" );
						addFlag( st.bSameFrame, "frcp" );
						addFlag( st.bCanRaise, "rais" );
						addFlag( st.bDehacked, "deh" );
						break;
					case 2:
						addFlag( st.bFast, "fast" );
						addFlag( st.bSlow, "slow" );
						addFlag( st.bFullbright, "lit" );
						addFlag( st.bNoDelay, "nodelay" );
						addFlag( st.bSameFrame, "sameframe" );
						addFlag( st.bCanRaise, "canraise" );
						addFlag( st.bDehacked, "deh-modified" );
						break;
					default:
						break;
				}

				outStr = outStr .. "\n" .. BreakPanelLines( tempFlagsStr );
			} // of if ( curstateFlagsType ) {}

			if ( curstateExtraInfo ) {
				tempFlagsStr = "";

				if ( st.UseFlags & SUF_ACTOR )
					tempFlagsStr = tempFlagsStr .. "actor";
				if ( st.UseFlags & SUF_OVERLAY )
					tempFlagsStr = tempFlagsStr .. ( tempFlagsStr != ""? ", " : "" ) .. "overlay";
				if ( st.UseFlags & SUF_WEAPON )
					tempFlagsStr = tempFlagsStr .. ( tempFlagsStr != ""? ", " : "" ) .. "weap";
				if ( st.UseFlags & SUF_ITEM )
					tempFlagsStr = tempFlagsStr .. ( tempFlagsStr != ""? ", " : "" ) .. "item";

				outStr = outStr .. DARKRED .. "\n Used by " .. GRAY .. tempFlagsStr .. DARKGRAY .. "\n TicRange " .. GRAY .. st.TicRange .. DARKGRAY .. ", misc " .. GRAY .. st.misc1 .. DARKGRAY .. "/" .. GRAY .. st.misc2;
			} // of if ( curstateExtraInfo ) {}


		} else if ( pspr ) { // Thanks to Il Str for crash reporting.
			PlayerInfo pl = weapOwner.player;

			outStr = outStr .. addPSpriteState( pl.FindPSprite( PSprite.WEAPON ) );

			// [McM] PSprite layers not finished and therefore not available in the ZChecker beta-versions.

			/*int pspriteOverlaysMode = pspriteOverlaysModeCVar.GetInt();
			int pspriteSpecialOverlayIDString = pspriteExtraOverlayIDCVar.GetString().ToInt();

			switch ( pspriteOverlaysMode ) {
				case 1: // Main overlay only.
					outStr = outStr .. addPSpriteState( pl.FindPSprite( PSprite.WEAPON ) );
					break;

				case 2: // Main + flash overlays.
					outStr = outStr .. addPSpriteState( pl.FindPSprite( PSprite.WEAPON ) ) 
							.. "\n" .. addPSpriteState( pl.FindPSprite( PSprite.FLASH ) );
					break;

				case 3: // Special overlay only.
					outStr = outStr .. addPSpriteState( pl.FindPSprite( pspriteSpecialOverlayIDString ) );
					break;

				case 4: // Main + special overlays.
					outStr = outStr .. addPSpriteState( pl.FindPSprite( PSprite.WEAPON ) ) 
							.. "\n" .. addPSpriteState( pl.FindPSprite( pspriteSpecialOverlayIDString ) );
					break;

				case 5: // All overlays.
					for ( ; pspr != NULL; pspr = pspr.Next )
						outStr = outStr .. addPSpriteState( pspr );
					break;

				default:
					break;
			} // of switch ( pspriteOverlaysMode ) {}
			*/

		} // of else if ( pspr ) {}

		return outStr;
	} // of override String GetInfo( Actor thing, ZCheckerField thingfield ) {}


	override String GetSubmenuLabel( OptionMenuDescriptor desc ) {
		ResetSubmenuItemsIterator();

		int statesAmount = GetNextSubmenuItemIntSelection( desc );
		bool statesLogging = !!GetNextSubmenuItemIntSelection( desc );

		String outlabel = ( statesAmount > 0? "" .. statesAmount : ZCGlobal.Locz( "PANELSUBLABEL_STATES_UNLIMITED" ) ) .. " " .. ZCGlobal.Locz( "PANELSUBLABEL_STATES_STATES" );

		if ( statesLogging )
			outlabel = outlabel .. ", " .. ZCGlobal.Locz( "PANELSUBLABEL_STATES_LOGGING" );

		return ZCGlobal.Locz( "PANELSUBMENU_DEFAULTLABEL" ) .. ZCGlobal.Locz( "PANELSUBLABEL_STATES_PREFIX" ) .. outlabel .. " " .. ZCGlobal.Locz( "PANELSUBMENU_DEFAULTMORE" );
	} // of override String GetSubmenuLabel( OptionMenuDescriptor desc ) {}

} // of class ZCStatesInfoPanel: ZCBaseInfoPanelPart {}


class ZCSpritesInfoPanel: ZCBaseInfoPanelPart {
	// For the compatibility with older versions:
	enum EExtraRenderStyles {
		STYLE_Multiply = STYLE_AddShaded + 1,
		STYLE_InverseMultiply,
		STYLE_ColorBlend,
		STYLE_Source,
		STYLE_ColorAdd,

		STYLE_Quantity
	};

	override void OnCreate( void ) {
		optionCVarName = "lldm_zcpanel_visual";
		optionLabel = "$ZCHECKER_INFOPANELS_VISUAL";
		optionLabelExtraHotkeys = "$ZCHECKER_INFOPANELKEYS_VISUAL";
	}

	protected String GetRenderStyleStr( int style ) {
		String str = "";

		switch ( style ) {
			case STYLE_None: str = "None"; break;
			case STYLE_Normal: str = "Normal"; break;
			case STYLE_Fuzzy: str = "Fuzzy"; break;
			case STYLE_SoulTrans: str = "SoulTrans"; break;
			case STYLE_OptFuzzy: str = "OptFuzzy"; break;
			case STYLE_Stencil: str = "Stencil"; break;
			case STYLE_Translucent: str = "Translucent"; break;
			case STYLE_Add: str = "Add"; break;
			case STYLE_Shaded: str = "Shaded"; break;
			case STYLE_TranslucentStencil: str = "TranslucentStencil"; break;
			case STYLE_Shadow: str = "Shadow"; break;
			case STYLE_Subtract: str = "Subtract"; break;
			case STYLE_AddStencil: str = "AddStencil"; break;
			case STYLE_AddShaded: str = "AddShaded"; break;
			case STYLE_Multiply: str = "Multiply"; break;
			case STYLE_InverseMultiply: str = "InverseMultiply"; break;
			case STYLE_ColorBlend: str = "ColorBlend"; break;
			case STYLE_Source: str = BLACK .. "(Source)"; break;
			case STYLE_ColorAdd: str = "ColorAdd"; break;
			default: str = BLACK .. "(Unknown: " .. style .. ")"; break;
		}

		return str;
	}

	override String GetInfo( Actor thing, ZCheckerField thingfield ) {
		String outStr = "";

		TextureID texture;
		bool textureBool;
		vector2 textureVec2;

		[ texture, textureBool, textureVec2 ] = thing.curstate.GetSpriteTexture( 1 );

		String textureName = TexMan.GetName( texture );

		int textureRealSizeX, textureRealSizeY;
		[ textureRealSizeX, textureRealSizeY ] = TexMan.GetSize( texture );

		vector2 textureSize = TexMan.GetScaledSize( texture );
		vector2 textureOffset = TexMan.GetScaledOffset( texture );

		outStr.AppendFormat( DARKGRAY .. "Alpha " .. LIGHTBLUE .. "%.3f" .. DARKGRAY .. ", render " .. GRAY .. "%s" .. DARKGRAY .. ".\n", thing.alpha, GetRenderStyleStr( thing.GetRenderStyle() ) );
		outStr.AppendFormat( DARKGRAY .. "Scale " .. DARKGREEN .. "(%+2.3f, %+2.3f)\n" .. DARKGRAY, thing.scale.x, thing.scale.y );
		outStr.AppendFormat( DARKGRAY .. "Size (" .. GRAY .. "%2.3f" .. BLUE .. "x" .. GRAY .. "%2.3f" .. DARKGRAY .. "); \nReal " .. textureRealSizeX .. BLUE .. "x" .. DARKGRAY .. textureRealSizeY .. " pixels.\n", textureSize.x, textureSize.y );
		outStr.AppendFormat( GRAY .. ( textureName.Mid( 5, 1 ) == "0"? "Faced" : "Rotor" ) .. DARKGRAY .. " ID %i, TranslID %i;\n", int( thing.sprite ), thing.Translation );
		outStr.AppendFormat( DARKGRAY .. "Txtr ofs " .. ZCGlobal.squeezeVector2dec( textureOffset, 2 ) .. ";\nbool %i vec2 " .. ZCGlobal.squeezeVector2dec( textureVec2, 2 ) .. ".", textureBool );

		return outStr;
	}
} // of class ZCSpritesInfoPanel: ZCBaseInfoPanelPart {}
