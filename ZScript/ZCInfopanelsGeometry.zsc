//
// Infopanels of the linked with actor level geometry: sectors and linedefs.
//

class ZCSectorsGeometryPanel: ZCBaseInfoPanelPart_PlayScope {
	String outinfo;
	Actor infothing;

	bool printSecLights, printSecTextures, printSecDamage, printSecPortal, printSecTerrain, printSecInteraction, printSecOther;
	int printSecFlagsMode, printSecHeightsMode;

	override void OnCreate_play( void ) {
		optionCVarName = "lldm_zcpanel_geomsectors";
		optionLabel = "$ZCHECKER_INFOPANELS_GEOMSECTORS";
		optionLabelExtraHotkeys = "$ZCHECKER_INFOPANELKEYS_GEOMSECTORS";
		optionSubmenuName = "ZChecker_InfopanelSubmenu_geomsectors";

		printSecLights = printSecTextures = printSecOther = printSecPortal = printSecTerrain = false;
	}

/*
	native readonly int WaterLevel;
	native double FloorClip;
	native Line BlockingLine;
*/

	Sector tempInfoSec;
	String tempFlagsStr;
	uint secFlagsAmount;

	static const String PortalTypesNames[] = {
		"Skybox", "StackThings", "Portal", "Portal", "EE-Plane", "EE-Horizon"
	};

	protected void addSecFlag( uint flag, String flagname ) {
		String flagcolor = ( ( ( secFlagsAmount / 5 ) & 1 )
						? ( ( tempInfoSec.flags & flag )? DARKGREEN : BLACK )
						: ( ( tempInfoSec.flags & flag )? GREEN : DARKGRAY ) );

		tempFlagsStr = tempFlagsStr .. flagcolor .. flagname .. " ";
		secFlagsAmount++;
	}

	protected void addSecFlag2( uint flag2, String flagname ) {
		String flagcolor = ( ( ( secFlagsAmount / 5 ) & 1 )
						? ( ( tempInfoSec.moreflags & flag2 )? DARKGREEN : BLACK )
						: ( ( tempInfoSec.moreflags & flag2 )? GREEN : DARKGRAY ) );

		tempFlagsStr = tempFlagsStr .. flagcolor .. flagname .. " ";
		secFlagsAmount++;
	}

	protected String getTextureInfo( int texpos ) {
		String outstr = "\"" .. CREAM .. tempInfoSec.GetTexture( texpos ) .. DARKGRAY .. "\"";

		vector2 texofs = ( tempInfoSec.GetXOffset( texpos ), tempInfoSec.GetYOffset( texpos ) );
		vector2 texscale = ( tempInfoSec.GetXScale( texpos ), tempInfoSec.GetYScale( texpos ) );
		double texangle = tempInfoSec.GetAngle( texpos );

		//if ( texofs.Length() != 0.0 || texscale.x != 1.0 || texscale.y != 1.0 || texangle != 0.0 )
			outstr.AppendFormat( BLACK .. "\n    scale" .. DARKGRAY .. "(" .. OLIVE .. "%.2f,%.2f" .. DARKGRAY .. ") " .. BLACK .. "+" .. DARKGRAY .. "(" .. PURPLE .. "%.2f,%.2f" .. DARKGRAY .. ");" ..
								 BLACK .. "\n    angle " .. DARKBROWN .. "%.2f" .. DARKGRAY, texscale.x, texscale.y, texofs.x, texofs.y, texangle );

		return outstr;
	}

	protected String getPortalInfo( int plane ) {
		Sector sec = tempInfoSec;
		String portalstr = FIRE .. "<unknown>" .. GRAY;

		int portaltype = sec.GetPortalType( sec.FLOOR );

		if ( portaltype >= 0 && portaltype <= SectorPortal.TYPE_HORIZON )
			portalstr = PortalTypesNames[ portaltype ];

		if ( sec.PortalIsLinked( plane ) )
			portalstr = "linked " .. portalstr .. DARKGRAY .. ";";

		String blocks = "";
		if ( sec.PortalBlocksView( plane ) ) {
			blocks = RED .. "view" .. DARKGRAY;
		}
		if ( sec.PortalBlocksSight( plane ) ) {
			if ( blocks != "" )
				blocks = ", ";

			blocks = blocks .. RED .. "sight" .. DARKGRAY;
		}
		if ( sec.PortalBlocksMovement( plane ) ) {
			if ( blocks != "" )
				blocks = ", ";

			blocks = blocks .. RED .. "move" .. DARKGRAY;
		}
		if ( sec.PortalBlocksSound( plane ) ) {
			if ( blocks != "" )
				blocks = ", ";

			blocks = blocks .. RED .. "sound" .. DARKGRAY;
		}

		portalstr = portalstr .. "\n  " .. ( blocks != ""? BRICK .. "Blocks " .. blocks : DARKGRAY .. "Blocks nothing" );

		vector3 portalOffsets = ( sec.GetPortalDisplacement( plane ), sec.GetPortalPlaneZ( plane ) );
		portalstr = portalstr .. ";\n  To grp " .. WHITE .. sec.GetOppositePortalGroup( plane ) .. DARKGRAY .. ": " .. ZCGlobal.squeezeVector3( portalOffsets );

		return portalstr;
	}

	protected void addSectorInfo( Sector sec, String sectorName = "", bool fromnewline = false ) {
		if ( !sec ) {
			outinfo.AppendFormat( BLACK .. "\n(No%s%s sector)" .. GRAY, ( sectorName == ""? "" : " " ), sectorName .. BLACK );
			return;
		}

		bool noextrainfo = true;

		String info = ( fromnewline? "\n" : "" ) .. DARKGRAY .. "Sector " .. GRAY .. sec.Index() .. DARKGRAY .. ( sectorName == ""? ":" : " (" .. sectorName .. DARKGRAY .. "):" );
		tempInfoSec = sec;

		double floorz, ceilingz;

		switch ( printSecHeightsMode ) {
			case 1:
				floorz = sec.floorplane.d * sec.floorplane.negiC;
				ceilingz = sec.ceilingplane.d * sec.ceilingplane.negiC;

				info.AppendFormat( "\n  Flr " .. DARKGREEN .. "%i" .. DARKGRAY .. "/Ceil " .. TAN .. "%i" .. DARKGRAY .. ".",
									int( floorz ), int( ceilingz ) );
				noextrainfo = false;
				break;

			case 2:
				floorz = infothing.floorz;
				ceilingz = infothing.ceilingz;

				info.AppendFormat( "\n  Ceil " .. TAN .. "%-4i" .. DARKGRAY .. " (head delta " .. TAN.. "%i" .. DARKGRAY .. ");\n"
								.. "  Flr " .. DARKGREEN .. "%-4i" .. DARKGRAY .. " (feet delta " .. DARKGREEN.. "%i" .. DARKGRAY .. ").",
								int( ceilingz ), int( ceilingz - infothing.pos.z - infothing.height ), int( floorz ), int( infothing.pos.z - floorz ) );
				noextrainfo = false;
				break;

			case 3:
				floorz = sec.floorplane.d * sec.floorplane.negiC;
				ceilingz = sec.ceilingplane.d * sec.ceilingplane.negiC;

				info.AppendFormat( "\n  Ceil " .. TAN .. "%-4i" .. DARKGRAY .. " (head delta " .. TAN.. "%i" .. DARKGRAY .. ");\n"
								.. "  Flr " .. DARKGREEN .. "%-4i" .. DARKGRAY .. " (feet delta " .. DARKGREEN.. "%i" .. DARKGRAY .. ").",
								int( ceilingz ), int( ceilingz - infothing.pos.z - infothing.height ), int( floorz ), int( infothing.pos.z - floorz ) );
				noextrainfo = false;
				break;

			case 0:
			default:
				break;
		}

		if ( printSecLights ) {
			int floorlight = sec.GetFloorLight();
			int ceillight = sec.GetCeilingLight();
			noextrainfo = false;

			if ( floorlight == sec.lightlevel && floorlight == ceillight )
				info = info .. "\n  Light level " .. WHITE .. sec.lightlevel .. DARKGRAY .. ".";
			else
				info = info .. "\n  Light " .. WHITE .. sec.lightlevel .. DARKGRAY .. ", flr " .. GRAY .. floorlight .. DARKGRAY .. ", ceil " .. GRAY .. ceillight .. ".";
		}

		if ( printSecTextures ) {
			noextrainfo = false;
			info = info .. "\n  Tex:" .. TAN .. "C " .. getTextureInfo( Sector.ceiling ) ..
						";\n  Tex:" .. DARKGREEN .. "F " .. getTextureInfo( Sector.floor ) .. ".";
		}

		if ( printSecDamage ) {
			// [McM]: now "sec.GetSpecial()" is not used.
			noextrainfo = false;

			info = info .. BRICK .. "\n  Damage" .. DARKRED .. ": type \"" .. ( sec.damageType == 'None'? DARKGRAY : ORANGE ) .. sec.damageType .. DARKRED .. "\",\n    " ..
				( sec.damageAmount == 0? DARKGRAY : BRICK ) .. sec.damageAmount .. DARKRED .. " hp/" .. BRICK .. sec.damageInterval .. DARKRED .. " tics, leakydmg " .. sec.leakydamage .. ".";
		}

		tempFlagsStr = "";
		secFlagsAmount = 0;

		switch ( printSecFlagsMode ) {
			case 1: // Short flag names.
				addSecFlag( Sector.SECF_SILENT, "qit" );
				addSecFlag( Sector.SECF_NOFALLINGDAMAGE, "-fald" );
				addSecFlag( Sector.SECF_FLOORDROP, "drop" );
				addSecFlag( Sector.SECF_NORESPAWN, "-rspw" );
				addSecFlag( Sector.SECF_FRICTION, "fric" );
				addSecFlag( Sector.SECF_PUSH, "p" );
				addSecFlag( Sector.SECF_SILENTMOVE, "mvqt" );
				addSecFlag( Sector.SECF_DMGTERRAINFX, "tfx" );
				addSecFlag( Sector.SECF_ENDGODMODE, "-god" );
				addSecFlag( Sector.SECF_ENDLEVEL, "endl" );
				addSecFlag( Sector.SECF_HAZARD, "stfhz" );
				addSecFlag( Sector.SECF_WASSECRET, "was9" );
				addSecFlag( Sector.SECF_SECRET, "is9" );
				addSecFlag( 2048, "-mna" ); // Not found in GZDoom 3.3.0.
				addSecFlag2( Sector.SECMF_FAKEFLOORONLY, "fflr" );
				addSecFlag2( Sector.SECMF_CLIPFAKEPLANES, "fclp" );
				addSecFlag2( Sector.SECMF_NOFAKELIGHT, "-flit" );
				addSecFlag2( Sector.SECMF_IGNOREHEIGHTSEC, "-hts" );
				addSecFlag2( Sector.SECMF_UNDERWATER, "wat" );
				addSecFlag2( Sector.SECMF_FORCEDUNDERWATER, "fwat" );
				addSecFlag2( Sector.SECMF_DRAWN, "vis" );
				addSecFlag2( Sector.SECMF_HIDDEN, "hid" );
				break;
			case 2: // Long flag names.
				addSecFlag( Sector.SECF_SILENT, "quiet" );
				addSecFlag( Sector.SECF_NOFALLINGDAMAGE, "-falldmg" );
				addSecFlag( Sector.SECF_FLOORDROP, "liftdrop" );
				addSecFlag( Sector.SECF_NORESPAWN, "-respawn" );
				addSecFlag( Sector.SECF_FRICTION, "friction" );
				addSecFlag( Sector.SECF_PUSH, "push" );
				addSecFlag( Sector.SECF_SILENTMOVE, "silentmove" );
				addSecFlag( Sector.SECF_DMGTERRAINFX, "terrainfx" );
				addSecFlag( Sector.SECF_ENDGODMODE, "-iddqd" );
				addSecFlag( Sector.SECF_ENDLEVEL, "endlevel" );
				addSecFlag( Sector.SECF_HAZARD, "strifehzrd" );
				addSecFlag( Sector.SECF_WASSECRET, "wassecret" );
				addSecFlag( Sector.SECF_SECRET, "newsecret" );
				addSecFlag( 2048, "-monsatk" ); // Not found in GZDoom 3.3.0.
				addSecFlag2( Sector.SECMF_FAKEFLOORONLY, "fakefloor" );
				addSecFlag2( Sector.SECMF_CLIPFAKEPLANES, "clipfakeplanes" );
				addSecFlag2( Sector.SECMF_NOFAKELIGHT, "-fakelit" );
				addSecFlag2( Sector.SECMF_IGNOREHEIGHTSEC, "-hghtsec" );
				addSecFlag2( Sector.SECMF_UNDERWATER, "water" );
				addSecFlag2( Sector.SECMF_FORCEDUNDERWATER, "waterforce" );
				addSecFlag2( Sector.SECMF_DRAWN, "alreadydrawn" );
				addSecFlag2( Sector.SECMF_HIDDEN, "hidden" );
				break;
			case 0:
			default:
				break;
		}

		if ( tempFlagsStr != "" ) {
			info = info .. "\n" .. BreakPanelLines( tempFlagsStr );
			noextrainfo = false;
		}

		if ( printSecPortal ) {
			bool floorportal = !!sec.Portals[ sec.FLOOR ];
			bool ceilportal = !!sec.Portals[ sec.CEILING ];

			info = info .. ICE .. "\n Portal group " .. sec.PortalGroup .. GRAY .. ( floorportal || ceilportal? ",\n" : ".\n" );

			if ( floorportal )
				info = info .. " Flr" .. DARKGREEN .. ": " .. getPortalInfo( sec.FLOOR ) .. GRAY .. ( ceilportal? ";\n" : ".\n" );

			if ( ceilportal )
				info = info .. " Ceil" .. TAN .. ": " .. getPortalInfo( sec.CEILING ) .. GRAY .. ".";

			noextrainfo = false;
		}

		if ( printSecInteraction ) {
			info.AppendFormat( DARKGRAY .. "\n Gravity %s%.4f%s;\n Frict %s%.2f%s, movefctr %s%.2f%s.",
							GRAY, sec.gravity, DARKGRAY,
							GRAY, sec.friction, DARKGRAY, GRAY, sec.movefactor, DARKGRAY );

			noextrainfo = false;
		}

		if ( noextrainfo )
			info.Replace( ":", "." );
		else
			RemoveLastNewlineSymbol( info );

		outinfo = outinfo .. info;
	} // of protected void addSectorInfo( Sector sec, String sectorName = "", bool fromnewline = false ) {}



	override String GetInfo_play( Actor thing, ZCheckerField thingfield ) {
		if ( thingfield.actorType != ZCheckerField.IAT_Normal )
			return "";

		outinfo = "";
		infothing = thing;

		bool differFloorCeilSectors = CVar.GetCVar( "lldm_zcpanel_var_geomsecdiffers", players[ consoleplayer ] ).GetBool();

		printSecHeightsMode = CVar.GetCVar( "lldm_zcpanel_var_geomsecheights", players[ consoleplayer ] ).GetInt();
		printSecLights = CVar.GetCVar( "lldm_zcpanel_var_geomseclight", players[ consoleplayer ] ).GetBool();
		printSecFlagsMode = CVar.GetCVar( "lldm_zcpanel_var_geomsecflags", players[ consoleplayer ] ).GetInt();
		printSecTextures = CVar.GetCVar( "lldm_zcpanel_var_geomsectex", players[ consoleplayer ] ).GetBool();
		printSecDamage = CVar.GetCVar( "lldm_zcpanel_var_geomsecdamage", players[ consoleplayer ] ).GetBool();
		printSecPortal = CVar.GetCVar( "lldm_zcpanel_var_geomsecportal", players[ consoleplayer ] ).GetBool();
		printSecInteraction = CVar.GetCVar( "lldm_zcpanel_var_geomsecphysics", players[ consoleplayer ] ).GetBool();
		printSecTerrain = CVar.GetCVar( "lldm_zcpanel_var_geomsecterrain", players[ consoleplayer ] ).GetBool();
		printSecOther = CVar.GetCVar( "lldm_zcpanel_var_geomsecother", players[ consoleplayer ] ).GetBool();

		if ( differFloorCeilSectors ) {
			if ( thing.cursector != thing.floorsector && thing.cursector != thing.ceilingsector ) {
				if ( thing.floorsector == thing.ceilingsector ) {
					addSectorInfo( thing.floorsector, "floor/ceil" );
				} else {
					addSectorInfo( thing.ceilingsector, "ceiling" );
					addSectorInfo( thing.floorsector, "floor", true );
				}
				addSectorInfo( thing.cursector, YELLOW .. "actual", true );

			} else {
				String header = "ceil";
				if ( thing.cursector == thing.ceilingsector )
					header = header .. ", " .. YELLOW .. "cur";

				addSectorInfo( thing.ceilingsector, header );

				header = "floor";
				if ( thing.cursector == thing.floorsector )
					header = header .. ", " .. YELLOW .. "cur";

				addSectorInfo( thing.floorsector, header, true );
			}
		} else {
			String cursecname = "";

			if ( thing.cursector == thing.floorsector )
				cursecname = YELLOW .. ( thing.cursector == thing.ceilingsector? " f/c" : " floor" );
			else if ( thing.cursector == thing.ceilingsector )
				cursecname = YELLOW .. " ceil";

			addSectorInfo( thing.cursector, "actual" .. cursecname );
		} // of else, of if ( differFloorCeilSectors ) {}


		if ( printSecTerrain ) {
			TerrainDef ter = thing.GetFloorTerrain();

			if ( ter ) {
				String C = DARKBROWN;

				String damageStr = "";
				if ( printSecDamage ) {
					damageStr = RED .. "  Damage" .. C .. ": type \"" .. ORANGE .. ter.DamageMOD .. C .. "\",\n    " ..
						RED .. ter.DamageAmount .. C .. " hp/" .. GRAY .. ter.DamageTimeMask .. C .. " tics. " .. ( ter.AllowProtection? DARKGREEN .. "Resistable" : ORANGE .. "No protect" ) .. "\n";
				}

				outinfo.AppendFormat( C ..
						"\nTerrain \"" .. TAN .. "%s" .. C .. "\" (" .. DARKGRAY .. "%i" .. C .. "):\n" ..
						"  Fric " .. TAN .. "%1.2f" .. C .. ", mul " .. TAN .. "%1.2f" .. C .. ", clipping " .. TAN .. "%1.2f\n" ..
						"  %s" .. C .. ", splash " .. GRAY .. "%i" .. C .. ".\n%s" .. C ..
						"  Steps: vol " .. GRAY .. "%1.2f, " .. C .. "tics " .. DARKGRAY .. "wlk" .. CYAN .. "%i" .. C .. "/" .. DARKGRAY .. "run" .. CYAN .. "%i" .. C .. ".\n"
						"    Left snd \"" .. TAN .. ter.LeftStepSound .. C .. "\";\n    Right snd \"" .. TAN .. ter.RightStepSound .. C .. "\".",
					ter.TerrainName, thing.floorterrain,
					ter.Friction, ter.MoveFactor, ter.FootClip,
					( ter.IsLiquid? DARKGRAY .. "Not liquid" : BLUE .. "Is liquid" ), ter.Splash,
					damageStr,
					ter.StepVolume, ter.WalkStepTics, ter.RunStepTics
				);
			} else {
				outinfo = outinfo .. DARKGRAY .. "\nNo terrain (" .. thing.floorterrain .. ").";
			}
		} // of if ( printSecTerrain ) {}

		return outinfo;
	}


	protected ui String fullOptionsString;
	protected ui int curOptionItemNum;
	protected ui bool fullOptionsStringOverflow;

	ui protected void addLabelFlag( bool optionval, String optionname ) {
		String localizedpart = ZCGlobal.Locz( "PANELSUBLABEL_GSEC_" .. optionname );

		if ( optionval && !fullOptionsStringOverflow ) {
			if ( fullOptionsString.Length() + localizedpart.Length() < 25 ) {
				if ( curOptionItemNum > 0 )
					fullOptionsString.AppendFormat( ", %s", localizedpart );
				else
					fullOptionsString = fullOptionsString .. localizedpart;

				curOptionItemNum++;
			} else {
				fullOptionsString = fullOptionsString .. ", ...";
				fullOptionsStringOverflow = true;
			}
		}
	} // of ui protected void addLabelFlag( bool optionval, String optionname ) {}

	override String GetSubmenuLabel( OptionMenuDescriptor desc ) {
		ResetSubmenuItemsIterator();

		bool separatedSectors = !!GetNextSubmenuItemIntSelection( desc );

		curOptionItemNum = 0;
		fullOptionsString = "";
		fullOptionsStringOverflow = false;

		addLabelFlag( GetNextSubmenuItemIntSelection( desc ), "HEIGHTS" );
		addLabelFlag( GetNextSubmenuItemIntSelection( desc ), "LIGHT" );
		addLabelFlag( GetNextSubmenuItemIntSelection( desc ), "TEXTURES" );
		addLabelFlag( GetNextSubmenuItemIntSelection( desc ), "FLAGS" );
		addLabelFlag( GetNextSubmenuItemIntSelection( desc ), "DAMAGE" );
		addLabelFlag( GetNextSubmenuItemIntSelection( desc ), "PHYSICS" );
		addLabelFlag( GetNextSubmenuItemIntSelection( desc ), "PORTALS" );
		addLabelFlag( GetNextSubmenuItemIntSelection( desc ), "TERRAIN" );
		addLabelFlag( GetNextSubmenuItemIntSelection( desc ), "OTHER" );

		if ( fullOptionsString.Length() != 0 )
			fullOptionsString = ". " .. ZCGlobal.Locz( "PANELSUBLABEL_GSEC_PREFIX2" ) .. fullOptionsString .. " ";

		fullOptionsString = ZCGlobal.Locz( "PANELSUBLABEL_GSEC_" .. ( separatedSectors? "FCSEC" : "CURSEC" ) ) .. fullOptionsString;

		return ZCGlobal.Locz( "PANELSUBMENU_DEFAULTLABEL" ) .. ZCGlobal.Locz( "PANELSUBLABEL_GSEC_PREFIX" ) .. fullOptionsString .. ZCGlobal.Locz( "PANELSUBMENU_DEFAULTMORE" );
	} // of override String GetSubmenuLabel( OptionMenuDescriptor desc ) {}

} // of class ZCSectorsGeometryPanel: ZCBaseInfoPanelPart_PlayScope {}



class ZCLinesGeometryPanel: ZCBaseInfoPanelPart_PlayScope {
	override void OnCreate_play( void ) {
		optionCVarName = "lldm_zcpanel_geomlines";
		optionLabel = "$ZCHECKER_INFOPANELS_GEOMLINES";
		optionLabelExtraHotkeys = "$ZCHECKER_INFOPANELKEYS_GEOMLINES";
		optionSubmenuName = "ZChecker_InfopanelSubmenu_geomlines";
	}

	override String GetInfo_play( Actor thing, ZCheckerField thingfield ) {
		return BLACK .. "([WIP] Linedefs geometry must be here).";
	}
} // of class ZCLinesGeometryPanel: ZCBaseInfoPanelPart_PlayScope {}
