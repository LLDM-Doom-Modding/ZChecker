//
// All CCMDs which alters or creates an actor.
//

class ZCGiveCommand: ZCConsoleCommand {
	Actor testee;
	EPositionParseStatus testeeStatus;
	String itemName;
	int itemAmount;

	override void InitCCMD() {
		SetupCCMDInterface(
			"zc_give",
			"zcgive [<pos>] <item> [<amount=1>]",
			"Give <amount> <item>s to the testee <pos> (or linetarget if omitted)",
			'ZCTooltipGiveCommand'
		);
	}

	override bool HandleArguments() {
		testeeStatus = PPS_Error;
		itemAmount = 1;
		itemName = "";

		bool outValue = false;

		if ( ccmd.args.Size() == 2 || ccmd.args.Size() == 3 )
			if ( !str2int_Ptr( ccmd.args[ ccmd.args.Size() - 1 ], +itemAmount ) )
				itemAmount = -1;

		if ( ccmd.args.Size() == 1 ) {
			itemName = ccmd.args[ 0 ];
			testee = LinetraceActor();
			outValue = true;
		} else if ( ccmd.args.Size() == 2 ) {
			if ( itemAmount != -1 ) {
				itemName = ccmd.args[ 0 ];
				outValue = true;
				testee = LinetraceActor();
			} else if ( PPS_Error != ( testeeStatus = getPlaceTestee( testee ) ) ) {
				itemAmount = 1;
				itemName = ccmd.args[ 1 ];
				outValue = true;
			}
		} else if ( ( ccmd.args.Size() == 3 ) && PPS_Error != ( testeeStatus = getPlaceTestee( testee ) ) ) {
			itemName = ccmd.args[ 1 ];
			outValue = true;
		}

		if ( outValue )
			itemName = ParseSpecialActorName( itemName, "Inventory" );

		return outValue && ( itemAmount != -1 );
	}

	override void DoCommand( void ) {
		if ( classnameParseStatus == CPS_RegexFailed )
			return;

		if ( testee ) {
			class<Actor> itemClass = itemName;

			if ( itemClass is 'Inventory' ) {
				if ( testee.A_GiveInventory( itemName, itemAmount ) )
					ZCGlobal.Log( LL_Detailed, "Item \"" .. itemClass.GetClassName() .. "\" is given to the actor " .. testee.GetClassName() .. " <" .. testee .. ">." );
				else
					ZCGlobal.Log( LL_Main, "Actor " .. testee.GetClassName() .. " cannot receive item." );
			} else {
				ZCGlobal.Log( LL_Main, "\"" .. itemName .. "\" isn't an inventory actor." );
			}
		} // of if ( testee ) {}

	}
} // of class ZCGiveCommand: ZCConsoleCommand {}

class ZCTakeCommand: ZCGiveCommand {
	override void InitCCMD() {
		SetupCCMDInterface(
			"zc_take",
			"zctake [<pos>] <item> [<amount=1>]",
			"Take <amount> <item>s from the testee <pos> (or linetarget if omitted)",
			'ZCTooltipTakeCommand'
		);
	}

	override void DoCommand( void ) {
		if ( classnameParseStatus == CPS_RegexFailed )
			return;

		if ( testee ) {
			class<Actor> itemClass = itemName;

			if ( itemClass is 'Inventory' ) {
				if ( testee.A_TakeInventory( itemName, itemAmount ) ) {
					/*if ( !GetDefaultByType( itemClass ).bKEEPDEPLETED ) {
						ZCGlobal.Log( LL_Detailed, "DESTROY Item \"" .. itemClass.GetClassName() .. "\" taken from the actor " .. testee.GetClassName() .. " <" .. testee .. ">." );
						Inventory inv = testee.FindInventory( itemName );
						inv.Destroy();
					}*/
					Inventory inv = testee.FindInventory( itemName );

					if ( inv && inv.amount == 0 )
						inv.Destroy();

					ZCGlobal.Log( LL_Detailed, "Item \"" .. itemClass.GetClassName() .. "\" is taken from the actor " .. testee.GetClassName() .. " <" .. testee .. ">." );

					/*if ( inv && !inv.bKEEPDEPLETED ) {
						ZCGlobal.Log( LL_Detailed, "Item \"" .. itemClass.GetClassName() .. "\" destroyed from the actor " .. testee.GetClassName() .. " <" .. testee .. ">." );
						inv.Destroy();
					} else if ( inv ) {
						ZCGlobal.Log( LL_Detailed, "Item \"" .. itemClass.GetClassName() .. "\" taken from the actor " .. testee.GetClassName() .. " <" .. testee .. ">." );
					}*/
				} else {
					ZCGlobal.Log( LL_Main, "Cannot take item from actor " .. testee.GetClassName() .. "." );
				}
			} else {
				ZCGlobal.Log( LL_Main, "\"" .. itemName .. "\" isn't an inventory actor." );
			}
		} // of if ( testee ) {}
	} // of override void DoCommand( void ) {}

} // of class ZCTakeCommand: ZCGiveCommand {}

class ZCActorActionCommand: ZCConsoleCommand {
	enum EActorActions {
		AC_Error = 0,
		AC_Revive,
		AC_Kill,
		AC_Destroy,
		AC_XKill,
	};

	Actor thing;
	EPositionParseStatus testeeStatus;
	EActorActions actoraction;

	override void InitCCMD() {
		SetupCCMDInterface(
			"zc_actoraction",
			"zcact [<pos>] <action>",
			"Do special complex action with actor on <pos> (or linetarget if omitted). Possible <action>s: \"rev[ive]\"|\"res[urrect]\", \"k[ill]\", \"xk[ill]\"|\"mdk\", \"destroy\"|\"rem[ove]\"|\"rm\"|\"del[ete]\""
		);
	}

	override bool HandleArguments() {
		testeeStatus = PPS_Error;
		bool outValue = false;

		if ( ccmd.args.Size() == 1 || ccmd.args.Size() == 2 ) {
			outValue = true;
			String what = ccmd.args[ ccmd.args.Size() - 1 ];
			what.ToLower();
			String what3chars = what.Left( 3 );

			if ( what3chars == "res" || what3chars == "rev" )
				actoraction = AC_Revive;
			else if ( what == "kill" || what == "k" || what == "die" )
				actoraction = AC_Kill;
			else if ( what == "xk" || what == "xkill" || what == "mdk" )
				actoraction = AC_XKill;
			else if ( what == "destroy" || what3chars == "rem" || what == "rm" || what3chars == "del" )
				actoraction = AC_Destroy;
			else
				outValue = false;

			if ( outValue ) {
				if ( ccmd.args.Size() == 1 ) {
					thing = LinetraceActor();
					outValue = true;
				} else {
					outValue = ( PPS_Error != ( testeeStatus = getPlaceTestee( thing ) ) );
				}
			}
		} // of if ( ccmd.args.Size() == 1 || ccmd.args.Size() == 2 ) {}

		return outValue;
	}

	protected void TryRevive( Actor thing ) {
		bool reviveBlocked = false;
		readonly<Actor> defactor = GetDefaultByType( thing.GetClass() );

		if ( defactor.bSOLID || defactor.bBLOCKEDBYSOLIDACTORS ) {
			BlockThingsIterator it = BlockThingsIterator.CreateFromPos( thing.pos.x, thing.pos.y, thing.pos.z, thing.height, thing.radius, false );

			while ( it.Next() && !reviveBlocked ) {
				if ( it.thing != thing && thing.Distance2D( it.thing ) < thing.radius && it.thing.bSOLID )
					reviveBlocked = true;
			}
		}

		String thingClassName = thing.GetClassName();

		if ( !reviveBlocked ) {
			// Compatibility with older versions.
			Actor prevMaster = thing.master;
			thing.master = thing;
			thing.A_RaiseMaster();

			if ( thing ) {
				thing.master = prevMaster;

				if ( thing.bCORPSE )
					LogLocalize2Str( LL_Main, "ZCACT_CANNOTREVIVE", thingClassName );
				else
					LogLocalize2Str( LL_Detailed, "ZCACT_REVIVED", thingClassName );
			} else {
				LogLocalize2Str( LL_Main, "ZCACT_REVIVEVANISH", thingClassName );
			}
		} else {
			LogLocalize2Str( LL_Main, "ZCACT_REVIVEBLOCKED", thingClassName );
		}
	}

	override void DoCommand( void ) {
		if ( !thing )
			return;

		Name thingname = thing.GetClassName();

		switch ( actoraction ) {
			case AC_Revive:
				TryRevive( thing );
				break;
			case AC_Kill:
				thing.A_Die();

				// Some actors are instantly destroyed in the "Death:" state sequence.
				if ( thing && thing.health > 0 )
					ZCGlobal.Log( LL_Main, "Cannot kill actor " .. thingname );
				else
					ZCGlobal.Log( LL_Detailed, "Killed actor " .. thingname );
				break;
			case AC_XKill:
				thing.DamageMobj( thing, thing, 0x7FFFFFF, 'None', DMSS_FOILINVUL | DMSS_NOFACTOR | DMSS_NOPROTECT );

				if ( thing && thing.health > 0 )
					ZCGlobal.Log( LL_Main, "Cannot extremely kill actor " .. thingname );
				else
					ZCGlobal.Log( LL_Detailed, "Extremely killed actor " .. thingname );
				break;
			case AC_Destroy:
				if ( RemoveWorldThinker( thing ) )
					ZCGlobal.Log( LL_Detailed, "Destroyed actor " .. thingname );
				break;
			default:
				ZCGlobal.Log( LL_Emergency, "Unknown action id \"" .. actoraction .. "\" for thing " .. thingname );
				break;
		}
	}
} // of class ZCActorActionCommand: ZCConsoleCommand {}


struct ZCSummonExtraParameters play {
	const SPECARGS_MAX = 5;
	const INFOPANEL_IGNORE = -2;
	const INFOPANEL_NOFREE = -1;

	int infopanelPos;

	double faceangle;
	bool isAngled;
	bool isFaceangleRelative;

	double facepitch;
	bool isFacepitchRelative;

	double zofs, forwardofs;

	int tid;
	int special;
	int specargs[ SPECARGS_MAX ];

	bool isFriend;
	bool isFoe;
	bool notProjectileRules;
	int spawnhp;
	int aggresiveness;
	bool forceInfight;
	bool cannotInfight;

	void Clear( void ) {
		infopanelPos = INFOPANEL_IGNORE;

		isAngled = false;
		isFaceangleRelative = isFacepitchRelative = false;
		faceangle = 0.0;
		facepitch = 0.0;
		zofs = forwardofs = 0.0;

		tid = special = 0;
		aggresiveness = -1;
		spawnhp = 0;

		for ( int i = 0; i < SPECARGS_MAX; i++ )
			specargs[ i ] = 0;

		isFriend = isFoe = notProjectileRules = forceInfight = cannotInfight = false;
	}

	void clampfloat( out double value, String valuedesc, double minlimit, double maxlimit ) {
		double tmpfloat = clamp( value, minlimit, maxlimit );

		if ( value != tmpfloat ) {
			ZCGlobal.Log( LL_Main, LLCOLOR_WARNING .. valuedesc .. " (" .. value .. ") isn't in range [" .. minlimit .. "; " .. maxlimit .. "]. " .. LLCOLOR_MAIN .. "Clamped to " .. tmpfloat );
			value = tmpfloat;
		}
	}

	void clampint( out int value, String valuedesc, int minlimit, int maxlimit ) {
		int tmpint = clamp( value, minlimit, maxlimit );

		if ( value != tmpint ) {
			ZCGlobal.Log( LL_Main, LLCOLOR_WARNING .. valuedesc .. " (" .. value .. ") isn't in range [" .. minlimit .. "; " .. maxlimit .. "]. " .. LLCOLOR_MAIN .. "Clamped to " .. tmpint );
			value = tmpint;
		}
	}

	void AdjustValues( void ) {
		faceangle %= 360.0;
		clampfloat( facepitch, "Pitch", -90.0, 90.0 );

		clampfloat( zofs, "Z offset", -32767.0, 32768.0 );
		clampfloat( forwardofs, "Forward offset", -32767.0, 32768.0 );

		clampint( special, "Actor special", 0, 255 );
		clampint( spawnhp, "Spawn health", 0, 0x7FFFFFFF );

		if ( aggresiveness != -1 )
			clampint( aggresiveness, "Aggresiveness", 0, 255 );

		for ( int i = 0; i < SPECARGS_MAX; i++ )
			clampint( specargs[ i ], "Arg[ " .. i .. " ]", 0, 255 );
	}
}; // of struct ZCSummonExtraParameters play {}

class ZCSummonCommand: ZCConsoleCommand {
	String summonee;
	int summoneeAmount;
	ZCSummonExtraParameters params;

	override void InitCCMD() {
		SetupCCMDInterface(
			"zc_summon",
			"zcsummon <class> [<amount=1>] [<comma-separated: [rel]ang,tid,spec,panel|add,arg0-4,zofs,fofs,hp,aggr (...:<value>), notproj,friend|foe,[no]infi>]",
			"Summon <amount> of <class>es on position by rules of build-in eponymous command. Extended info (<extras>) may be provided by comma-separated (\",\") list. Possible parameters are:\n"
				.. "  BEHAVIOUR: f[riend]|foe, aggr[essiveness]:<dflt55>, [no]infi[ght], notproj[ectile];\n"
				.. "  WORLD    : [rel]ang[le]:<angle>, [rel]pitch:<pitch>, zofs:<mappixels>, f[wd]ofs:<mappixels>;\n"
				.. "  SPECIAL  : panel|add[:<pos>], tid:<TID>, spec:<special>, a[rg]0:<arg0>,...,a[rg]4:<arg4>, hp|health:<hp>.",
			'ZCTooltipSummonCommand'
		);
	}

	override bool HandleArguments() {
		summoneeAmount = 1;
		summonee = "";
		params.Clear();

		bool outValue = false;
		int argsSize = ccmd.args.Size();
		String paramstr = "";

		if ( argsSize > 0 ) {
			summonee = ccmd.args[ 0 ];

			if ( argsSize > 1 ) {
				if ( str2int_Ptr( ccmd.args[ 1 ], +summoneeAmount ) ) {
					if ( argsSize == 3 )
						paramstr = ccmd.args[ 2 ];
				} else {
					paramstr = ccmd.args[ 1 ];
				}
			}

			outValue = true;

			if ( paramstr != "" ) {
				Array<String> paramlist;
				paramstr.ToLower();
				paramstr.Split( paramlist, ",", TOK_SKIPEMPTY );

				for ( int i = 0; i < paramlist.Size(); i++ ) {
					String curparam = paramlist[ i ];
					int colonIndex = -1;

					// Flags:
					if ( curparam == "f" || curparam.Left( 2 ) == "fr" ) {
						params.isFriend = true;
						params.isFoe = false;
					} else if ( curparam == "foe" || curparam == "enemy" ) {
						params.isFriend = false;
						params.isFoe = true;
					} else if ( curparam == "notprj" || curparam.Left( 7 ) == "notproj" ) {
						params.notProjectileRules = true;
					} else if ( curparam.Left( 4 ) == "infi" ) {
						params.forceInfight = true;
						params.cannotInfight = false;
					} else if ( curparam.Left( 6 ) == "noinfi" ) {
						params.forceInfight = false;
						params.cannotInfight = true;

					// Arguments:
					} else if ( curparam.Left( 3 ) == "add" || curparam.Left( 5 ) == "panel" ) {
						if ( -1 != ( colonIndex = curparam.IndexOf( ":" ) ) || -1 != ( colonIndex = curparam.IndexOf( "=" ) ) ) {
							String panelposStr = curparam.Mid( colonIndex + 1, curparam.Length() - colonIndex - 1 );

							if ( panelposStr == "" ) {
								params.infopanelPos = ZCAddCommand.FindFreeTesteePlace( ccmd );
							} else {
								int panelposNum;

								if ( str2int_Ptr( panelposStr, +panelposNum ) && checkTesteePlace( --panelposNum ) )
									params.infopanelPos = panelposNum;
								else
									ZCGlobal.Log( LL_Main, LLCOLOR_WARNING .. "Wrong panel number \"" .. panelposStr .. "\"." );
							}
						} else {
							params.infopanelPos = ZCAddCommand.FindFreeTesteePlace( ccmd );
						}

					} else if ( -1 != ( colonIndex = curparam.IndexOf( ":" ) ) || -1 != ( colonIndex = curparam.IndexOf( "=" ) ) ) {
						if ( colonIndex < curparam.Length() ) {
							String paramdiv[ 2 ];
							paramdiv[ 0 ] = curparam.Left( colonIndex );
							paramdiv[ 1 ] = curparam.Mid( colonIndex + 1, curparam.Length() - colonIndex - 1 );

							int paramvalue;
							int paramnamelen = paramdiv[ 0 ].Length();

							if ( str2int_Ptr( paramdiv[ 1 ], +paramvalue, true ) ) {
								if ( paramdiv[ 0 ] == "ang" || paramdiv[ 0 ] == "angle" ) {
									params.faceangle = paramvalue;
									params.isFaceangleRelative = false;
									params.isAngled = true;
								} else if ( paramdiv[ 0 ] == "relang" || paramdiv[ 0 ] == "relangle" || paramdiv[ 0 ] == "ra" || paramdiv[ 0 ] == "relativeang" || paramdiv[ 0 ] == "relativeangle" || paramdiv[ 0 ] == "rang" || paramdiv[ 0 ] == "rangle" ) {
									params.faceangle = paramvalue;
									params.isFaceangleRelative = true;
									params.isAngled = true;

								} else if ( paramdiv[ 0 ] == "pitch" ) {
									params.facepitch = paramvalue;
									params.isFacepitchRelative = false;
								} else if ( paramdiv[ 0 ] == "relpitch" || paramdiv[ 0 ] == "relativepitch" ) {
									params.facepitch = paramvalue;
									params.isFacepitchRelative = true;

								} else if ( paramdiv[ 0 ] == "tid" ) {
									params.tid = paramvalue;
								} else if ( paramdiv[ 0 ] == "hp" || paramdiv[ 0 ] == "health" ) {
									params.spawnhp = paramvalue;
								} else if ( paramdiv[ 0 ] == "spec" || paramdiv[ 0 ] == "special" ) {
									params.special = paramvalue;
								} else if ( paramdiv[ 0 ] == "z" || paramdiv[ 0 ] == "zofs" || paramdiv[ 0 ] == "zoffset" ) {
									params.zofs = paramvalue;
								} else if ( paramdiv[ 0 ] == "fwd" || paramdiv[ 0 ] == "fofs" || paramdiv[ 0 ] == "foffset" || paramdiv[ 0 ] == "fwdofs" || paramdiv[ 0 ] == "fwdoffset" || paramdiv[ 0 ] == "forwardofs" || paramdiv[ 0 ] == "forwardoffset" ) {
									params.forwardofs = paramvalue;
								} else if ( paramdiv[ 0 ].Left( 4 ) == "aggr" ) {
									params.aggresiveness = paramvalue;

								} else if ( ( paramnamelen == 2 && paramdiv[ 0 ].Left( 1 ) == "a" ) || ( paramnamelen == 4 && paramdiv[ 0 ].Left( 3 ) == "arg" ) ) {
									int argnum = paramdiv[ 0 ].Mid( paramnamelen - 1, 1 ).CharCodeAt( 0 ) - 0x30;

									if ( argnum >= 0 && argnum < params.SPECARGS_MAX )
										params.specargs[ argnum ] = paramvalue;
									else
										ZCGlobal.Log( LL_Main, LLCOLOR_WARNING .. "Wrong argument number for parameter \"" .. paramdiv[ 0 ] .. "\"." );

								} else {
									ZCGlobal.Log( LL_Main, LLCOLOR_WARNING .. "Unknown parameter \"" .. curparam .. "\"." );
								}
							} else {
								ZCGlobal.Log( LL_Main, LLCOLOR_WARNING .. "Value \"" .. paramdiv[ 1 ] .. "\" for parameter \"" .. paramdiv[ 0 ] .. "\" isn't a digit." );
							} // of else of if ( str2int_Ptr( paramdiv[ 1 ], +paramvalue ) ) {}

						} else {
							ZCGlobal.Log( LL_Main, LLCOLOR_WARNING .. "No value " .. LLCOLOR_MAIN .. "for parameter \"" .. curparam .. "\"." );
						} // of else of if ( colonIndex < curparam.Length() ) {}

					} else {
						ZCGlobal.Log( LL_Main, LLCOLOR_WARNING .. "Unknown flag \"" .. curparam .. "\"." );
					}

				} // of for ( int i = 0; i < paramlist.Size(); i++ ) {}
			} // of if ( paramstr != "" ) {}

		} // of if ( argsSize > 0 ) {}


		if ( outValue ) {
			summonee = ParseSpecialActorName( ccmd.args[ 0 ] );
			params.AdjustValues();
		}

		return outValue;
	}

	protected virtual void PostActorSummon( Actor summoned, Actor splayer ) {
		if ( !summoned )
			return;

		if ( params.isAngled )
			summoned.angle = ( params.isFaceangleRelative? summoned.AngleTo( splayer ) : 0.0 ) + params.faceangle;


		double newpitch = params.facepitch;

		if ( params.isFacepitchRelative ) {
			newpitch += splayer.pitch;
			params.clampfloat( newpitch, "Relative pitch", -90.0, 90.0 );
		}

		summoned.pitch = newpitch;


		if ( params.isFriend )
			summoned.bFRIENDLY = true;
		else if ( params.isFoe )
			summoned.bFRIENDLY = false;

		if ( params.forceInfight ) {
			summoned.bFORCEINFIGHTING = true;
			summoned.bNOINFIGHTING = false;
		} else if ( params.cannotInfight ) {
			summoned.bFORCEINFIGHTING = false;
			summoned.bNOINFIGHTING = true;
		}

		if ( params.aggresiveness != -1 )
			summoned.MinMissileChance = 255 - params.aggresiveness;

		if ( params.spawnhp != 0 )
			summoned.health = params.spawnhp;

		if ( params.tid )
			summoned.ChangeTid( params.tid );
		if ( params.special )
			summoned.special = params.special;

		for ( int i = 0; i < params.SPECARGS_MAX; i++ )
			if ( params.specargs[ i ] != 0 )
				summoned.args[ i ] = params.specargs[ i ];
	}

	override void DoCommand( void ) {
		if ( classnameParseStatus == CPS_RegexFailed )
			return;

		if ( summoneeAmount > 200 && !ZCGlobal.DangerousCCMDsAllowed( "summoning more than 200 actors" ) ) {
			ZCGlobal.Log( LL_Main, "Trying to summon more than 200 actors in once. " .. LLCOLOR_DARK .. "You may disable this limit switching the \"Allow dangerous actions\" option." );
			return;
		}

		class<Actor> summoneeClass = summonee;

		if ( summoneeClass ) {
			// See GZDoom source code -> "src/d_net.cpp" -> "Net_DoCommand()".
			Actor player = players[ ccmd.e.Player ].mo;

			if ( player ) {
				readonly<Actor> summoneeDef = GetDefaultByType( summoneeClass );
				Actor summoned = NULL;

				if ( !params.notProjectileRules && summoneeDef.bMISSILE ) {
					vector3 spawnpos = player.Vec3Angle( summoneeDef.radius * 2 + player.radius + params.forwardofs, player.angle, 8.0 + params.zofs );

					for ( int i = 0; i < summoneeAmount; i++ ) {
						summoned = player.SpawnPlayerMissile( summoneeClass );

						if ( summoned ) {
							vector2 dirFromPlayer = player.Vec2To( summoned );
							summoned.SetOrigin( summoned.pos + ( dirFromPlayer / dirFromPlayer.Length() * params.forwardofs, params.zofs ), false );

							PostActorSummon( summoned, player );
						}
					}
				} else {
					vector3 spawnpos = player.Vec3Angle( summoneeDef.radius * 2 + player.radius + params.forwardofs, player.angle, 8.0 + params.zofs );

					for ( int i = 0; i < summoneeAmount; i++ )
						PostActorSummon( summoned = Actor.Spawn( summoneeClass, spawnpos, ALLOW_REPLACE ), player );
				} // of else of if ( GetDefaultByType( summoneeClass ).bMISSILE ) {}

				if ( summoned ) {
					ZCGlobal.Log( LL_Detailed, "Summoned " .. summoneeAmount .. " actor" .. ( summoneeAmount > 1? "s " : " " ) .. TEXTCOLOR_DARKGRAY .. "of class \"" .. TEXTCOLOR_GREEN .. summoneeClass.GetClassName() .. TEXTCOLOR_DARKGRAY .. "\"" );

					if ( params.infopanelPos == params.INFOPANEL_NOFREE ) {
						ZCGlobal.Log( LL_Main, "Cannot add actor to list, insufficient place. Try set target to list directly via parameter \"panel:1\"..\"panel:" .. MaxTestee .. "\"." );

					} else if ( params.infopanelPos != params.INFOPANEL_IGNORE ) {
						// No duplicate fields check when summoning new actors, of course.

						ZCheckerField field = ccmd.handler.Testee[ params.infopanelPos ];

						field.mo = summoned;
						field.UpdateActorMetainformation( ccmd );

						if ( summoneeAmount > 1 )
							ZCGlobal.Log( LL_Main, LLCOLOR_DARK .. "Only last summoned actor added to the list." );
					}
				} else {
					ZCGlobal.Log( LL_Main, TEXTCOLOR_BRICK .. "Cannot summon actor class " .. TEXTCOLOR_DARKGRAY .. "\"" .. TEXTCOLOR_GRAY .. summoneeClass.GetClassName() .. TEXTCOLOR_DARKGRAY .. "\". It may be abstract or immediately destroyed in the BeginPlay() override." );
				}
			} // of if ( player ) {}

		} else {
			ZCGlobal.Log( LL_Main, "\"" .. summonee .. "\" isn't an actor class." );
		}
	}
} // of class ZCSummonCommand: ZCConsoleCommand {}

class ZCCopyPasteClass_dummy: ZCConsoleCommand {
	override void InitCCMD() {
		SetupCCMDInterface(
			"",
			"zccopyclass;zccopysummon [<amount=1>] [<comma-separated list as in \"zcsummon\">]",
			"A paired commands. \"zccopyclass\" saves a linetarget actor class into $savedclass internal variable; \"zccopysummon\" summons a $savedclass actor(s)."
		);
	}
}


class ZCSetFlagCommand: ZCConsoleCommand {
	Actor testee;
	EPositionParseStatus testeeStatus;
	String flagName;
	int flagSetType;
	bool skipKnownFlags;
	bool callACSSetFlag;
 
	override void InitCCMD() {
		SetupCCMDInterface(
			"zc_setflag",
			"zcsetflag [<pos>] <flag name> [<value: 0/f[alse]/reset or 1/t[rue]/set>[,nospec]];zcsetf [<pos>] <flag name> [<value>[,nospec]];zcsf [<pos>] <flag name> [<value>[,nospec]];zcbud [<pos>] [<value>]",
			"Set/reset a <flag> for the testee in <pos> (or linetarget actor if omitted). If no <value> provided, toggles a <flag>. Note: because of asynchronous ZScript netevent and ACS machine execution result in singleplayer for the most of the flags will be delayed for one tic.",
			'ZCTooltipSetFlagCommand'
		);
	}

	override bool HandleArguments() {
		flagSetType = -1;
		skipKnownFlags = false;
//zcsetflag [<pos>] <flagname> [<type>][,nospec[ecial]|raw]
		bool outValue = false;

		int ccmdArgsSize = ccmd.args.Size();

		if ( ccmdArgsSize == 2 || ccmdArgsSize == 3 ) {
			String optionstr[ 2 ];
			optionstr[ 0 ] = ccmd.args[ ccmdArgsSize - 1 ];
			optionstr[ 0 ].ToLower();

			int delimIndex = optionstr[ 0 ].IndexOf( "," );

			if ( delimIndex == -1 )
				delimIndex = optionstr[ 0 ].IndexOf( "=" );

			if ( delimIndex == -1 )
				delimIndex = optionstr[ 0 ].IndexOf( ":" );

			if ( delimIndex != -1 ) {
				optionstr[ 1 ] = optionstr[ 0 ].Mid( delimIndex + 1, optionstr[ 0 ].Length() - delimIndex - 1 );
				optionstr[ 0 ].Truncate( delimIndex );
			} else {
				optionstr[ 1 ] = "";
			}

			for ( int i = 0; i < 2; i++ ) {
				if ( optionstr[ i ] == "" )
					continue;

				if ( optionstr[ i ] == "raw" || optionstr[ i ] == "ns" || optionstr[ i ].Left( 6 ) == "nospec" || optionstr[ i ] == "acs" )
					skipKnownFlags = true;
				else
					flagSetType = getBoolFromStr( optionstr[ i ] );
			}
		} // of if ( ccmdArgsSize == 2 || ccmdArgsSize == 3 ) {}

		if ( ccmdArgsSize == 1 ) {
			flagName = ccmd.args[ 0 ];
			outValue = true;
			testee = LinetraceActor();

		} else if ( ccmdArgsSize == 2 ) {
			if ( flagSetType != -1 || skipKnownFlags ) {
				flagName = ccmd.args[ 0 ];
				outValue = true;
				testee = LinetraceActor();
			} else if ( PPS_Error != ( testeeStatus = getPlaceTestee( testee ) ) ) {
				flagName = ccmd.args[ 1 ];
				outValue = true;
			}

		} else if ( ( ccmdArgsSize == 3 ) && ( PPS_Error != ( testeeStatus = getPlaceTestee( testee ) ) ) ) {
			flagName = ccmd.args[ 1 ];
			outValue = ( flagSetType != -1 ) || skipKnownFlags;
		}

		//console.printf( TEXTCOLOR_DARKGRAY .. GetClassName() .. "HandleArguments(). zcStatus: " .. testeeStatus .. " (actor " .. ZCGlobal.ObjectName( testee ) .. "), flagName: " .. flagName .. ", flagSetType: " .. flagSetType .. ".  outValue: " .. outValue .. "." );

		return outValue;
	}

	private bool setFlag( bool origflag ) {
		callACSSetFlag = false;

		if ( flagSetType == -1 )
			flagSetType = !origflag;

		return flagSetType;
	}

	// I have no idea how to convert a sequence of Strings to flag names less crooked...
	override void DoCommand( void ) {
		if ( !testee )
			return;

		callACSSetFlag = true;

		if ( !skipKnownFlags ) {
			flagName.ToUpper();

			if ( flagName == "BUDDHA" )
				testee.bBUDDHA = setFlag( testee.bBUDDHA );
			else if ( flagName == "NODAMAGE" )
				testee.bNODAMAGE = setFlag( testee.bNODAMAGE );
			else if ( flagName == "NOINTERACTION" )
				testee.bNOINTERACTION = setFlag( testee.bNOINTERACTION );
			else if ( flagName == "SOLID" )
				testee.bSOLID = setFlag( testee.bSOLID );
			else if ( flagName == "SHOOTABLE" )
				testee.bSHOOTABLE = setFlag( testee.bSHOOTABLE );
			else if ( flagName == "FLOAT" )
				testee.bFLOAT = setFlag( testee.bFLOAT );
			else if ( flagName == "NOGRAVITY" )
				testee.bNOGRAVITY = setFlag( testee.bNOGRAVITY );
			else if ( flagName == "FRIENDLY" )
				testee.bFRIENDLY = setFlag( testee.bFRIENDLY );
			else if ( flagName == "DORMANT" )
				testee.bDORMANT = setFlag( testee.bDORMANT );
			else if ( flagName == "NOTARGET" )
				testee.bNOTARGET = setFlag( testee.bNOTARGET );
			else if ( flagName == "FORCEINFIGHTING" )
				testee.bFORCEINFIGHTING = setFlag( testee.bFORCEINFIGHTING );
			else if ( flagName == "NOINFIGHTING" )
				testee.bNOINFIGHTING = setFlag( testee.bNOINFIGHTING );
			else if ( flagName == "INVULNERABLE" )
				testee.bINVULNERABLE = setFlag( testee.bINVULNERABLE );
			else if ( flagName == "BRIGHT" )
				testee.bBRIGHT = setFlag( testee.bBRIGHT );
			else if ( flagName == "NONSHOOTABLE" )
				testee.bNONSHOOTABLE = setFlag( testee.bNONSHOOTABLE );
			else if ( flagName == "NOBLOCKMAP" )
				testee.A_ChangeLinkFlags( blockmap: setFlag( testee.bNOBLOCKMAP ) );
			else if ( flagName == "NOSECTOR" )
				testee.A_ChangeLinkFlags( sector: setFlag( testee.bNOSECTOR ) );

			if ( !callACSSetFlag )
				ZCGlobal.Log( LL_Detailed, "Flag \"" .. flagName .. "\" for actor " .. testee.GetClassName() .. " is succesfully set to " .. flagSetType );
		} // of if ( !skipKnownFlags ) {}


		if ( callACSSetFlag ) {
			let storage = ZCZScriptACSStringsAPI.Get();
			storage.AddString( storage.APIST_Flags, flagName );

			// Cannot get ACS asynchronous "SetResultValue()" here.
			testee.ACS_NamedExecuteAlways( "ZChecker_CCMD_SetFlag", 0, flagSetType );
		}
	} // of override void DoCommand( void ) {}

	static String ACS_GetFlagName( Actor activator ) {
		return ZCZScriptACSStringsAPI.PopStringStatic( ZCZScriptACSStringsAPI.APIST_Flags );
	}
} // of class ZCSetFlagCommand: ZCConsoleCommand {}

class ZCSetStateCommand: ZCConsoleCommand {
	EPositionParseStatus testeeStatus;
	Actor testee;
	String newState;

	override void InitCCMD() {
		SetupCCMDInterface(
			"zc_setstate",
			"zcsetstate [<pos>] <state label>;zcsets[t] [<pos>] <state label>;zcst [<pos>] <state label>",
			"Set a state to <state label> for the testee in <pos> (or actor on line of sight if omitted). Note: because of asynchronous ZScript/ACS execution result in singleplayer will be delayed for one tic."
		);
	}

	override bool HandleArguments() {
		testeeStatus = PPS_Error;
		bool outValue = false;

		if ( ccmd.args.Size() == 1 ) {
			newState = ccmd.args[ 0 ];
			outValue = true;
			testee = LinetraceActor();
		} else if ( ccmd.args.Size() == 2 && PPS_Error != ( testeeStatus = getPlaceTestee( testee ) ) ) {
			newState = ccmd.args[ 1 ];
			outValue = true;
		}

		return outValue;
	}

	// I have no idea how to convert a sequence of Strings to StateLabel less crooked, too...
	override void DoCommand( void ) {
		if ( testee ) {
			let storage = ZCZScriptACSStringsAPI.Get();
			storage.AddString( storage.APIST_States, newState );

			// Cannot get ACS asynchronous "SetResultValue()" here.
			testee.ACS_NamedExecuteAlways( "ZChecker_CCMD_SetState", 0 );
		}
	}

	static String ACS_GetStateName( Actor activator ) {
		return ZCZScriptACSStringsAPI.PopStringStatic( ZCZScriptACSStringsAPI.APIST_States );
	}
} // of class ZCSetStateCommand: ZCConsoleCommand {}




struct ZCSetPropertyParameterDouble {
	double value;
	bool changed;

	double valclamp( String valuedesc, double minlimit, double maxlimit ) {
		if ( !changed )
			return value;

		double tmpfloat = clamp( value, minlimit, maxlimit );

		if ( value != tmpfloat ) {
			ZCGlobal.ClearscopeLog( LL_Main, String.Format( LLCOLOR_WARNING .. "%s (%i) isn't in range [%i; %i]. " .. LLCOLOR_MAIN .. "Clamped to %i.", valuedesc, value, minlimit, maxlimit, tmpfloat ) );
			value = tmpfloat;
		}

		return value;
	}

	void set( in double newval ) {
		value = newval;
		changed = true;
	}

	void setfor( out double what ) {
		if ( changed )
			what = value;
	}
} // of struct ZCSetPropertyParameterDouble {}

struct ZCSetPropertyParameterInteger {
	int value;
	bool changed;

	double valclamp( String valuedesc, int minlimit, int maxlimit ) {
		if ( !changed )
			return value;

		int tmpfloat = clamp( value, minlimit, maxlimit );

		if ( value != tmpfloat ) {;
			ZCGlobal.ClearscopeLog( LL_Main, String.Format( LLCOLOR_WARNING .. "%s (%.3d) isn't in range [%.3d; %.3d]. " .. LLCOLOR_MAIN .. "Clamped to %.3d.", valuedesc, value, minlimit, maxlimit, tmpfloat ) );
			value = tmpfloat;
		}

		return value;
	}

	void set( in int newval ) {
		value = newval;
		changed = true;
	}

	void setfor( out int what ) {
		if ( changed )
			what = value;
	}
} // of struct ZCSetPropertyParameterInteger {}

struct ZCSetPropertyParameterPointer {
	Actor value; // [striked] See "ZCConsoleCommand.EPositionParseStatus".
	bool changed;

	void set( in Actor newval ) {
		value = newval;
		changed = true;
	}

	void setfor( out Actor what ) {
		if ( changed )
			what = value;
	}
}


struct ZCSetPropertyExtraParameters play {
	enum EPropertyFieldsDoubles {
		PFD_X = 0,	// Coordinates.
		PFD_Y,
		PFD_Z,
		PFD_VelX,	// Current velocity.
		PFD_VelY,
		PFD_VelZ,
		PFD_Angle,	// Tilts.
		PFD_Pitch,
		PFD_Roll,
		PFD_Radius,	// Interactions.
		PFD_Height,
		PFD_Speed,
		PFD_FloatSpeed,
		PFD_Gravity,
		PFD_ScaleX,	// Visual.
		PFD_ScaleY,
		PFD_Alpha,

		PFD_Quantity
	}

	enum EPropertyFieldsIntegers {
		PFI_Health = 0,
		PFI_Mass,
		PFI_Arg0,
		PFI_Arg1,
		PFI_Arg2,
		PFI_Arg3,
		PFI_Arg4,
		PFI_Special,
		PFI_TID,

		PFI_Quantity,

		PFI_Args_Start		= PFI_Arg0,
		PFI_Args_End 		= PFI_Arg4 + 1,
	}

	enum EPropertyFieldsPointers {
		PFP_Target = 0,
		PFP_Master,
		PFP_Tracer,

		PFP_Quantity
	}

	ZCSetPropertyParameterDouble[ PFD_Quantity ] doubles;
	ZCSetPropertyParameterInteger[ PFI_Quantity ] integers;
	ZCSetPropertyParameterPointer[ PFP_Quantity ] pointers;


	void Clear( void ) {
		for ( int i = 0; i < PFD_Quantity; i++ )
			doubles[ i ].changed = false;

		for ( int i = 0; i < PFI_Quantity; i++ )
			integers[ i ].changed = false;

		for ( int i = 0; i < PFP_Quantity; i++ )
			pointers[ i ].changed = false;
	}

	void AdjustValues( void ) {
		static const double PFD_Data_MinLimits[] = {
			-9999999999.9, -9999999999.9, -9999999999.9,
			-32767.0, -32767.0, -32767.0,
			-36000.0, -90.0, -36000.0,
			0.0, 0.0, 0.0, 0.0, 0.0,
			-32767.0, -32767.0, 0.0
		};
		static const double PFD_Data_MaxLimits[] = {
			9999999999.9, 9999999999.9, 9999999999.9,
			32767.0, 32767.0, 32767.0,
			36000.0, 90.0, 36000.0,
			32767.0, 32767.0, 32767.0, 32767.0, 32767.0,
			32767.0, 32767.0, 1.0
		};
		static const String PFD_Data_LimitsDescriptions[] = {
			"X", "Y", "Z",
			"velX", "velY", "velZ",
			"angle", "pitch", "roll",
			"radius", "height", "speed", "float speed", "gravity",
			"scaleX", "scaleY", "alpha"
		};
		static const String PFP_Data_PtrDescriptions[] = {
			"target", "master", "tracer"
		};


		for ( int i = 0; i < PFD_Quantity; i++ )
			doubles[ i ].valclamp( PFD_Data_LimitsDescriptions[ i ], PFD_Data_MinLimits[ i ], PFD_Data_MaxLimits[ i ] );

		doubles[ PFD_Angle ].value %= 360.0;
		doubles[ PFD_Roll ].value %= 360.0;

		integers[ PFI_Health ].valclamp( "Health", -0x7FFFFFFF, 0x7FFFFFFF );
		integers[ PFI_Special ].valclamp( "Special/action", 0, 65535 );

		for ( int i = PFI_Args_Start; i < PFI_Args_End; i++ )
			integers[ i ].valclamp( "Args[ " .. ( i - PFI_Args_Start ) .. " ]", 0, 255 );

		integers[ PFI_TID ].valclamp( "TID", 0, 2147483647 );
	} // of void AdjustValues( void ) {}

}; // of struct ZCSetPropertyExtraParameters play {}

class ZCSetPropertyCommand: ZCConsoleCommand {
	enum EPropertySetMode {
		PSM_Error = -2,
		PSM_WrongRelative = -1,
		PSM_Raw = 0,
		PSM_Add,
		PSM_Relative,
	};

	Actor alteree;
	Actor relativeTo; // May be an alteree actor itself.

	EPositionParseStatus testeeStatus;
	EPropertySetMode setmode;
	ZCSetPropertyExtraParameters params;

	override void InitCCMD() {
		SetupCCMDInterface(
			"zc_setproperty",
			"zcsetprop [<pos>] [add|rel[ative][:<pos>]|raw] <property>:<value>[,<property>:<value>,[...]];zcsetp [<pos>] [add|rel[:<pos>]] <property>:<value>[,...];zcprop [<pos>] [add|rel[:<pos>]] <property>:<value>[,...];zcsp [<pos>] [add|rel[:<pos>]] <property>:<value>[,...];zchate [<pos>] [<pos_newtarget>|null]",

			"Sets a <property> to <value> for the actor in position <pos> (or for the linetarget if omitted). By default or with a \"raw\" parameter all properties will be simply overwritten; with \"add\" they will be added to the current values (if it's possible); and with \"rel[ative]][:<pos>]\" will be added relative to the specified actor's properties or to caller player if <pos> is omitted (also if it is possible). Recognizable properties are:\n"
				.. "  WORLD   : x <float>, y <float>, z <float>|floor|ceil|rawceil, velx|vely|velz <float>, angle|pitch|roll <float>;\n"
				.. "  INTERACT: health|hp <int>|s[pawn]|max, [float]speed <float>, radius <float>, height <float>, mass <int>, gravity <float>;\n"
				.. "  VISUAL  : scale[x|y] <float>, alpha <float>;\n"
				.. "  POINTERS: target|tp <pos>|null, master|mp <pos>|null, tracer|trp|rp <pos>|null;\n"
				.. "  SPECIAL : arg0..arg4 <int>, special|action <int>, tid <val>|unique."
		);
	}

	private void handlePointer( int pointer_id, String propvalue ) {
		if ( propvalue != "" ) {
			Actor newptr;

			if ( propvalue == "null" || propvalue == "none" || propvalue == "clear" ) {
				params.pointers[ pointer_id ].set( NULL );
			} else {
				getPlaceTestee( newptr, 0, propvalue );

				if ( newptr ) 
					params.pointers[ pointer_id ].set( newptr );
			}
		} else {
			params.pointers[ pointer_id ].set( players[ ccmd.e.Player ].mo );
		}
	} // of private void handlePointer( int pointer_id, String pvalue ) {}

	override bool HandleArguments() {
		bool outValue = false;
		int ccmdArgsSize = ccmd.args.Size();
		alteree = relativeTo = NULL;

		if ( ccmdArgsSize == 1 ) {
			// Set properties for the linetarget.
			alteree = LinetraceActor();
			outValue = true;

		} else if ( ccmdArgsSize > 1 ) {
			String modeargstr = ccmd.args[ ccmdArgsSize - 2 ];
			modeargstr.ToLower();

			// Set mode parsing:
			setmode = PSM_Raw;

			if ( modeargstr == "add" || modeargstr == "+" ) {
				setmode = PSM_Add;

			} else if ( modeargstr.Left( 3 ) == "rel" ) {
				setmode = PSM_WrongRelative;

				int delimIndex = modeargstr.IndexOf( "=" );

				if ( delimIndex == -1 )
					delimIndex = modeargstr.IndexOf( ":" );

				if ( delimIndex != -1 ) {
					String relactorstr = modeargstr.Mid( delimIndex + 1, modeargstr.Length() - delimIndex - 1 );

					if ( ( testeeStatus = getPlaceTestee( relativeTo, specialstring: relactorstr ) ) > PPS_WrongPos )
						setmode = PSM_Relative;

				} else {
					relativeTo = players[ ccmd.e.Player ].mo;
					setmode = PSM_Relative;
					ZCGlobal.Log( LL_Detailed, LLCOLOR_DARK .. "Base actor for relative properties " .. LLCOLOR_MAIN .. "is set to caller player" .. LLCOLOR_DARK .. "." );
				}

			} else if ( modeargstr == "raw" ) {
				setmode = PSM_Raw;

			} else {
				setmode = PSM_Error;
			}

			// In general an alteree actor place parsing:
			if ( ccmdArgsSize == 3 || setmode == PSM_Error ) {
				if ( PPS_Error != ( testeeStatus = getPlaceTestee( alteree, 0 ) ) )
					outValue = true;
				else
					ZCGlobal.Log( LL_Main, LLCOLOR_WARNING .. "Unknown keyword " .. LLCOLOR_MAIN .. "\"" .. modeargstr .. "\"." );
			} else {
				alteree = LinetraceActor();
				outValue = true;
			}
		} // of else if ( ccmdArgsSize > 1 ) {}


		// Parse properties/parameters string:
		if ( outValue ) {
			params.Clear();

			Array<String> paramlist;
			ccmd.args[ ccmdArgsSize - 1 ].ToLower();
			ccmd.args[ ccmdArgsSize - 1 ].Split( paramlist, ",", TOK_SKIPEMPTY );

			for ( int i = 0; i < paramlist.Size(); i++ ) {
				String curparam = paramlist[ i ];
				curparam.ToLower();

				int delimIndex = curparam.IndexOf( "=" );

				if ( delimIndex == -1 )
					delimIndex = curparam.IndexOf( ":" );

				if ( delimIndex == -1 ) {
					ZCGlobal.Log( LL_Main, LLCOLOR_WARNING .. "No delimiter symbol" .. LLCOLOR_MAIN .. " (\":\" or \"=\") " .. LLCOLOR_WARNING .. "provided" .. LLCOLOR_MAIN .. " for property \"" .. curparam .. "\"." );
					continue;
				}

				String prop = curparam.Left( delimIndex );
				String pvalue = curparam.Mid( delimIndex + 1, curparam.Length() - delimIndex - 1 );


				if ( prop == "target" || prop == "targ" || prop == "tp" ) {
					handlePointer( params.PFP_Target, pvalue );

				} else if ( prop == "master" || prop == "mp" ) {
					handlePointer( params.PFP_Master, pvalue );

				} else if ( prop == "tracer" || prop == "trp" || prop == "rp" ) {
					handlePointer( params.PFP_Tracer, pvalue );

				} else {
					double paramdouble;

					// Some parameters (like "posz") has extra values (like "floor").
					if ( str2double_Ptr( pvalue, +paramdouble ) ) {
						int paramint;

						if ( prop == "angle" || prop == "ang" ) {
							params.doubles[ params.PFD_Angle ].set( paramdouble );
						} else if ( prop == "pitch" ) {
							params.doubles[ params.PFD_Pitch ].set( paramdouble );
						} else if ( prop == "roll" ) {
							params.doubles[ params.PFD_Roll ].set( paramdouble );
						} else if ( prop == "x" || prop == "posx" ) {
							params.doubles[ params.PFD_X ].set( paramdouble );
						} else if ( prop == "y" || prop == "posy" ) {
							params.doubles[ params.PFD_Y ].set( paramdouble );
						} else if ( prop == "z" || prop == "posy" ) {
							params.doubles[ params.PFD_Z ].set( paramdouble );
						} else if ( prop == "vx" || prop == "velx" ) {
							params.doubles[ params.PFD_VelX ].set( paramdouble );
						} else if ( prop == "vy" || prop == "vely" ) {
							params.doubles[ params.PFD_VelY ].set( paramdouble );
						} else if ( prop == "vz" || prop == "vely" ) {
							params.doubles[ params.PFD_VelZ ].set( paramdouble );
						} else if ( prop == "radius" || prop == "rad" ) {
							params.doubles[ params.PFD_Radius ].set( paramdouble );
						} else if ( prop == "height" ) {
							params.doubles[ params.PFD_Height ].set( paramdouble );
						} else if ( prop == "speed" ) {
							params.doubles[ params.PFD_Speed ].set( paramdouble );
						} else if ( prop == "floatspeed" || prop == "flspeed" || prop == "fspeed" ) {
							params.doubles[ params.PFD_FloatSpeed ].set( paramdouble );
						} else if ( prop == "gravity" || prop == "grav" ) {
							params.doubles[ params.PFD_Gravity ].set( paramdouble );
						} else if ( prop == "scale" ) {
							params.doubles[ params.PFD_ScaleX ].set( paramdouble );
							params.doubles[ params.PFD_ScaleY ].set( paramdouble );
						} else if ( prop == "scalex" ) {
							params.doubles[ params.PFD_ScaleX ].set( paramdouble );
						} else if ( prop == "scaley" ) {
							params.doubles[ params.PFD_ScaleY ].set( paramdouble );
						} else if ( prop == "alpha" ) {
							params.doubles[ params.PFD_Height ].set( paramdouble );

						// Integer properties:
						} else if ( str2int_Ptr( pvalue, +paramint, true ) ) {
							int propnamelen = prop.Length();

							if ( prop == "health" || prop == "hp" || prop == "h" ) {
								params.integers[ params.PFI_Health ].set( paramint );
							} else if ( prop == "mass" ) {
								params.integers[ params.PFI_Mass ].set( paramint );
							} else if ( prop == "tid" ) {
								params.integers[ params.PFI_TID ].set( paramint );
							} else if ( prop == "action" || prop == "special" || prop == "spec" ) {
								params.integers[ params.PFI_Special ].set( paramint );

							} else if ( ( propnamelen == 2 && prop.Left( 1 ) == "a" ) || ( propnamelen == 4 && prop.Left( 3 ) == "arg" ) ) {
								int argnum = prop.Mid( propnamelen - 1, 1 ).CharCodeAt( 0 ) - 0x30;

								if ( argnum >= 0 && argnum < ZCSummonExtraParameters.SPECARGS_MAX )
									params.integers[ argnum + params.PFI_Arg0 ].set( paramint );
								else
									ZCGlobal.Log( LL_Main, LLCOLOR_WARNING .. "Wrong argument number in parameter \"" .. prop .. "\"." );

							} else {
								ZCGlobal.Log( LL_Main, LLCOLOR_WARNING .. "Unknown integer property \"" .. prop .. "\"." );
							}

						} else {
							ZCGlobal.Log( LL_Main, LLCOLOR_WARNING .. "Unknown float-point property \"" .. prop .. "\"." );
						}

					} else {
						Actor baseactor = ( relativeTo? relativeTo : alteree );

						if ( prop == "z" || prop == "posz" ) {
							String pvalueLeft1 = pvalue.Left( 1 );

							if ( pvalueLeft1 == "f" )
								params.doubles[ params.PFD_Z ].set( baseactor.floorz );
							else if ( pvalueLeft1 == "c" )
								params.doubles[ params.PFD_Z ].set( baseactor.ceilingz - alteree.height );
							else if ( pvalue.Left( 4 ) == "rawc" || pvalue.Left( 2 ) == "hc" )
								params.doubles[ params.PFD_Z ].set( baseactor.ceilingz );
							else
								ZCGlobal.Log( LL_Main, LLCOLOR_WARNING .. "Unknown placeholder \"" .. pvalue .. "\" for the Z coordinate property. " .. LLCOLOR_DARK .. "Must be " .. LLCOLOR_MAIN .. "<float>" .. LLCOLOR_DARK .. ", " .. LLCOLOR_MAIN .. "f[loor] " .. LLCOLOR_DARK .. ", " .. LLCOLOR_MAIN .. "c[eil]" .. LLCOLOR_DARK .. " or " .. LLCOLOR_MAIN .. "rawc[eil]|hc" .. LLCOLOR_DARK );

						} else if ( prop == "health" || prop == "hp" || prop == "h" ) {
							if ( pvalue.Left( 1 ) == "s" )
								params.integers[ params.PFI_Health ].set( baseactor.GetSpawnHealth() );
							else if ( pvalue == "max" )
								params.integers[ params.PFI_Health ].set( 0x7FFFFFFF );
							else
								ZCGlobal.Log( LL_Main, LLCOLOR_WARNING .. "Unknown placeholder \"" .. pvalue .. "\" for the health property. " .. LLCOLOR_DARK .. "Must be " .. LLCOLOR_MAIN .. "<int>" .. LLCOLOR_DARK .. ", " .. LLCOLOR_MAIN .. "max" .. LLCOLOR_DARK .. " or " .. LLCOLOR_MAIN .. "s[pawn]" .. LLCOLOR_DARK );

						} else if ( prop == "tid" && ( pvalue.Left( 2 ) == "un" || pvalue == "new" ) ) {
							params.integers[ params.PFI_TID ].set( Actor.FindUniqueTid() );

						} else if ( pvalue == "" ) {
							ZCGlobal.Log( LL_Main, LLCOLOR_DARK .. "Skipped empty property \"" .. prop .. "\"." );

						} else {
							// In fast, this line is a last frontier when no input matches found at all.
							ZCGlobal.Log( LL_Main, LLCOLOR_WARNING .. "Unknown property " .. LLCOLOR_MAIN .. "\"" .. prop .. ":" .. pvalue .. "\"." );
						}
					} // of else of if ( str2double_Ptr( paramdiv[ 1 ], +paramdouble ) ) {}

				} // of else of {} else if ( prop == "tracer" || prop == "trp" || prop == "rp" ) {}

			} // of for ( int i = 0; i < paramlist.Size(); i++ ) {}

		} // of if ( outValue ) {}


		if ( setmode == PSM_Add )
			relativeTo = alteree;

		return outValue;
	}

	override void DoCommand( void ) {
		if ( !alteree )
			return;

		ZCGlobal.Log( LL_Main, LLCOLOR_DARK .. "[Work-in-progress, not all of the properties can be changed correctly or be changed at all]." );

		// Valid in "PSM_Add" and "PSM_Relative" set modes.
		if ( relativeTo ) {
			params.integers[ params.PFI_Health ].value += relativeTo.health;
			params.integers[ params.PFI_Mass ].value += relativeTo.health;

			//params.integers[ params.PFI_Special ].value += relativeTo.special;
			//params.integers[ params.PFI_TID ].value += relativeTo.tid;
			//for ( int i = 0; i < ZCSummonExtraParameters.SPECARGS_MAX; i++ )
			//	params.integers[ params.PFI_Args_Start + i ].value += relativeTo.args[ i ];

			params.doubles[ params.PFD_X ].value += relativeTo.pos.x;
			params.doubles[ params.PFD_Y ].value += relativeTo.pos.y;
			params.doubles[ params.PFD_Z ].value += relativeTo.pos.z;
			params.doubles[ params.PFD_VelX ].value += relativeTo.vel.x;
			params.doubles[ params.PFD_VelY ].value += relativeTo.vel.y;
			params.doubles[ params.PFD_VelZ ].value += relativeTo.vel.z;

			params.doubles[ params.PFD_Angle ].value += relativeTo.angle;
			params.doubles[ params.PFD_Pitch ].value += relativeTo.pitch;
			params.doubles[ params.PFD_Roll ].value += relativeTo.roll;
			params.doubles[ params.PFD_Radius ].value += relativeTo.radius;
			params.doubles[ params.PFD_Height ].value += relativeTo.height;
			params.doubles[ params.PFD_Speed ].value += relativeTo.speed;
			params.doubles[ params.PFD_FloatSpeed ].value += relativeTo.floatspeed;
			params.doubles[ params.PFD_Gravity ].value += relativeTo.gravity;
			params.doubles[ params.PFD_ScaleX ].value += relativeTo.scale.x;
			params.doubles[ params.PFD_ScaleY ].value += relativeTo.scale.y;
			params.doubles[ params.PFD_Alpha ].value += relativeTo.alpha;
		}


		// Clamping etc.:
		params.AdjustValues();


		// Changing:
		params.pointers[ params.PFP_Target ].setfor( alteree.target );
		params.pointers[ params.PFP_Master ].setfor( alteree.master );
		params.pointers[ params.PFP_Tracer ].setfor( alteree.tracer );

		params.integers[ params.PFI_Health ].setfor( alteree.health );
		params.integers[ params.PFI_Mass ].setfor( alteree.mass );
		params.integers[ params.PFI_Special ].setfor( alteree.special );

		//ZCGlobal.Log( LL_Main, "HP for " .. alteree.GetClassName() .. ": must be " .. params.integers[ params.PFI_Health ].value .. ", is " .. alteree.health );

		if ( params.integers[ params.PFI_TID ].changed )
			alteree.ChangeTid( params.integers[ params.PFI_TID ].value );

		for ( int i = 0; i < ZCSummonExtraParameters.SPECARGS_MAX; i++ )
			params.integers[ params.PFI_Args_Start + i ].setfor( alteree.args[ i ] );

		vector3 altereeNewPos = (
					params.doubles[ params.PFD_X ].changed? params.doubles[ params.PFD_X ].value : alteree.pos.x,
					params.doubles[ params.PFD_Y ].changed? params.doubles[ params.PFD_Y ].value : alteree.pos.y,
					params.doubles[ params.PFD_Z ].changed? params.doubles[ params.PFD_Z ].value : alteree.pos.z
				);
		alteree.SetOrigin( altereeNewPos, false );
		params.doubles[ params.PFD_VelX ].setfor( alteree.vel.x );
		params.doubles[ params.PFD_VelY ].setfor( alteree.vel.y );
		params.doubles[ params.PFD_VelZ ].setfor( alteree.vel.z );

		params.doubles[ params.PFD_Angle ].setfor( alteree.angle );
		params.doubles[ params.PFD_Pitch ].setfor( alteree.pitch );
		params.doubles[ params.PFD_Roll ].setfor( alteree.roll );
		alteree.A_SetSize( 	params.doubles[ params.PFD_Radius ].changed? params.doubles[ params.PFD_Radius ].value : -1,
							params.doubles[ params.PFD_Height ].changed? params.doubles[ params.PFD_Height ].value : -1, false );
		params.doubles[ params.PFD_Speed ].setfor( alteree.speed );
		params.doubles[ params.PFD_FloatSpeed ].setfor( alteree.floatspeed );
		params.doubles[ params.PFD_Gravity ].setfor( alteree.gravity );
		params.doubles[ params.PFD_ScaleX ].setfor( alteree.scale.x );
		params.doubles[ params.PFD_ScaleY ].setfor( alteree.scale.y );
		params.doubles[ params.PFD_Alpha ].setfor( alteree.alpha );
	}
} // of class ZCSetPropertyCommand: ZCConsoleCommand {}
