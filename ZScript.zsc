version "2.4.0"

#include "ZScript/ZCMapScripts.zsc"
#include "ZScript/ZCBaseClasses.zsc"
#include "ZScript/ZCMenus.zsc"

#include "ZScript/ZCCommandsGeneral.zsc"
#include "ZScript/ZCCommandsActorAlter.zsc"
#include "ZScript/ZCCommandsInfo.zsc"
#include "ZScript/ZCCommandsMap.zsc"

#include "ZScript/ZCInfoPanelMain.zsc"
#include "ZScript/ZCInfoPanelPointers.zsc"
#include "ZScript/ZCInfoPanelOther.zsc"


const MaxTestee = 3;
const ZCheckerVersion = "0.68";

// AutoAmbient: 0x7FAA0004.
const ZCheckerHandlerOrder = 0x7FAA0005;
const ZCheckerMapHandlerOrder = 0x7FAA0006;



enum EZCheckerLogLevels {
	LL_Emergency = 0,
	LL_General = 1,
	LL_Detailed,
	LL_Debug,

	LL_Common = LL_General,
	LL_Normal = LL_General,
	LL_Main = LL_General,
	LL_Extended = LL_Detailed,
	LL_Critical = LL_Emergency,

	LL_FlagsMask	= 0xFFF0,
	LL_NoDot		= 0x4000,
	LL_NoPrefix		= 0x8000
};

enum EZCheckerInfoActorType {
	IAT_Normal = 0,
	IAT_StateProvider
};

enum ENewlineTypes {
	ZCNL_Unknown = 0,
	ZCNL_DOS,			// 0x0D, 0x0A
	ZCNL_Unix,			// 0x0A
	ZCNL_Mac,			// 0x0D
	ZCNL_DOSReversed	// 0x0A, 0x0D
};

const LLCOLOR_MAIN = "\c-";
const LLCOLOR_DEBUG = TEXTCOLOR_DARKGRAY;
const LLCOLOR_EMERGENCY = TEXTCOLOR_FIRE;

const CCMDCOLOR_TITLE = TEXTCOLOR_GREEN;
const CCMDCOLOR_TITLEWIP = TEXTCOLOR_DARKGRAY;
const CCMDCOLOR_DESC = "\c-";


class ZCGlobal: Thinker {
	int zcCurLogLevel;

	static void UpdateLogLevel( void ) {
		ZCGlobal.Get().zcCurLogLevel = CVar.FindCVar( "mcm_zchecker_loglevel" ).GetInt();
	}

	static play ZCGlobal Get( void ) {
		ZCGlobal it = ZCGlobal( ThinkerIterator.Create( "ZCGlobal", STAT_STATIC ).Next() );

		if ( !it ) {
			// Creating and initializating a singleton global object:
			it = new( "ZCGlobal" );
			it.ChangeStatNum( STAT_STATIC );
			UpdateLogLevel(); // This one calls "Get()" again, yeah...
		}

		return it;
	} // of static play ZCGlobal Get( void ) {}

	// Prints well-decorated information string:
	static clearscope void ClearscopeLog( EZCheckerLogLevels loglevel, String text ) {
		String logPrefix = "";

		if ( !( loglevel & LL_NoPrefix ) ) {
			switch ( loglevel & ~LL_FlagsMask ) {
				case LL_Debug:
					logPrefix = LLCOLOR_DEBUG .. "[ZC Debug] ";
					break;
				case LL_Emergency:
					logPrefix = TEXTCOLOR_RED .. "[ZChecker emergency] " .. LLCOLOR_EMERGENCY;
					break;
				default:
					logPrefix = TEXTCOLOR_GRAY .. "[ZC] " .. LLCOLOR_MAIN;
					break;
			} // of switch ( loglevel & ~LL_NoPrefix ) {
		} // of if ( !( loglevel & LL_NoPrefix ) ) {

		if ( !( loglevel & LL_NoDot ) ) {
			String lastch = text.Mid( text.Length() - 1 );

			if ( !( lastch == "." || lastch == "!" || lastch == "?" || lastch == ":" || lastch == ";" ) )
				text = text .. ".";
		}

		console.printf( logPrefix .. text );
	} // of static clearscope void ClearscopeLog( EZCheckerLogLevels loglevel, String text ) {}

	// A play-scope wrapper around a data-scope method (mostly used):
	static play void Log( EZCheckerLogLevels loglevel, String text ) {
		if ( ZCGlobal.Get().zcCurLogLevel >= ( loglevel & ~LL_FlagsMask ) )
			ClearscopeLog( loglevel, text );
	}

	static ui void UILog( EZCheckerLogLevels loglevel, String text ) {
		ZCGlobal it = ZCGlobal( ThinkerIterator.Create( "ZCGlobal", STAT_STATIC ).Next() );

		if ( it && ( it.zcCurLogLevel >= ( loglevel & ~LL_FlagsMask ) ) )
			ClearscopeLog( loglevel, text );
	}

	// Test for the special ZChecker level:
	static clearscope bool IsEverythingLevel( void ) {
		// Got from "mapchecksum everything" CCMD:
		//return level.GetChecksum() ~== "8FBB774BE1D239B5C7CB514DF9B41A2E" || ( CVar.FindCVar( "mcm_zchecker_forceevmap" ).GetBool() );

		return ( level.mapname ~== "EVERYTHING" ) || ( level.mapname ~== "EVERYTHING_SIMPLED" )
			|| ( CVar.FindCVar( "mcm_zchecker_ev_forcemap" ).GetBool() );
	}


    static clearscope String squeezeVector2( vector2 vec ) {
        return "(" .. int( vec.x ) .. ", " .. int( vec.y ) .. ")";
    }

    static clearscope String squeezeVector3( vector3 vec ) {
        return "(" .. int( vec.x ) .. ", " .. int( vec.y ) .. ", " .. int( vec.z ) .. ")";
    }


	static double, double vector3ToAnglePitch( vector3 vec ) {
		return atan2( vec.y, vec.x ), -90.0 * sin( vec.z );
	}

	static vector3 anglePitchToVector3( double angle, double pitch ) {
		double cosangle = cos( angle );
		double cospitch = cos( pitch );

		return ( cosangle * cospitch, sin( angle ) * cospitch, sin( pitch ) );
	}

	// Remove all first and all last spaces and tabs in the string.
	static String TruncateStringBySpaces( String str ) {

		uint code = str.CharCodeAt( 0 );
		while ( ( code == 9 || code == 11 || code == 32 ) && str.Length() ) {
			str = str.Mid( 1 );
			code = str.CharCodeAt( 0 );
		}

		uint len = str.Length() - 1;
		code = str.CharCodeAt( len );
		while ( ( code == 9 || code == 11 || code == 32 ) && ( len + 1 ) ) {
			str = str.Left( len );
			len = str.Length() - 1;
			code = str.CharCodeAt( len );
		}

		return str;
	}
} // of class ZCGlobal: Thinker {}



class ZCheckerHandler: StaticEventHandler {
	Array<ZCConsoleClass> zcConsoleHandleClasses;
	Array<ZCBaseInfoPanelPart> zcInfoPanelClasses;

	// It's impossible to avoid unloading actors (and pointers to them) from 
	//memory on level reloading. Or I just don't know something.
	ZCheckerField Testee[ MaxTestee ];

	ZCheckerCCMDInfo zcCCMDInfo;
	transient Font zcInfoPanelFont;

	String zcConfigFile;

	ZCConsoleClass GetZCConsoleClass( int index ) {
		ZCConsoleClass ccmd = NULL;

		if ( index >= 0 && index < zcConsoleHandleClasses.Size() ) {
			ccmd = zcConsoleHandleClasses[ index ];
			//ZCGlobal.Log( LL_Debug, GetClassName() .. "::GetZCConsoleClass(). CCMD #" .. index .. " is <" .. zcConsoleHandleClasses[ index ] .. ">." );
		}

		return ccmd;
	} // of ZCConsoleClass GetZCConsoleClass( int index ) {}

	private bool addConsoleClass( class<ZCConsoleClass> classname, bool importantFlag = false ) {
		ZCGlobal.Log( LL_Debug, GetClassName() .. "::addConsoleClass(). Trying to add \"" .. ( classname? classname.GetClassName() .. "" : "<NULL>" ) .. "\"." );

		if ( classname ) {
			ZCConsoleClass newCCMD = ZCConsoleClass.Create( classname, importantFlag );
			zcConsoleHandleClasses.push( newCCMD );
			newCCMD.PostBeginPlay();
		}

		return !!classname;
	}
	private void addConsoleSeparator( bool importantFlag = false ) {
		zcConsoleHandleClasses.push( ZCConsoleClass.Create( 'ZCConsoleSeparator', importantFlag ) );
	}

	private bool addInfoPanelClass( class<ZCBaseInfoPanelPart> classname ) {
		ZCGlobal.Log( LL_Debug, GetClassName() .. "::addInfoPanelClass(). Trying to add \"" .. ( classname? classname.GetClassName() .. "" : "<NULL>" ) .. "\"." );

		if ( classname ) {
			ZCBaseInfoPanelPart newPanelPart = ZCBaseInfoPanelPart.Create( classname, self );
			zcInfoPanelClasses.push( newPanelPart );
		}

		return !!classname;
	}


	static ENewlineTypes GetTextNewlinesType( String text ) {
		ENewlineTypes nltype = ZCNL_Unknown;

		if ( text != "" ) {
			int curPos = 0;
			int textLength = text.Length();

			while ( nltype == ZCNL_Unknown && curPos < textLength ) {
				if ( text.CharCodeAt( curPos ) == 0x0D )
					nltype = ( ++curPos < textLength && text.CharCodeAt( curPos ) == 0x0A )? ZCNL_DOS : ZCNL_Mac;
				else if ( text.CharCodeAt( curPos ) == 0x0A )
					nltype = ( ++curPos < textLength && text.CharCodeAt( curPos ) == 0x0D )? ZCNL_DOSReversed : ZCNL_Unix;

				curPos++;
			}

		} // of if ( text != "" ) {}

		return nltype;
	} // of static ENewlineTypes GetTextNewlinesType( String text ) {}

	private void handleConfigFileNewlines( String newlineTypeComment, String replaceeNewlineSeq ) {
		ZCGlobal.Log( LL_Debug, "New lines type for file \"ZChecker.cfg\" recognized as " .. newlineTypeComment );

		if ( replaceeNewlineSeq != "\n" )
			zcConfigFile.Replace( replaceeNewlineSeq, "\n" );
	}

	private bool checkKeywordArgsAmount( uint argsAmount, String keyword, uint rightAmount, String argsDescription ) {
		if ( argsAmount != rightAmount ) {
			ZCGlobal.Log( LL_Emergency, GetClassName() .. "::checkKeywordArgsAmount(). Wrong amount of arguments for keyword \"" .. keyword .. "\": has " .. argsAmount .. ", must be " .. rightAmount .. " (" .. argsDescription .. ")." );
			return false;
		}

		return true;
	}

	void IncludeConsoleClasses( void ) {
        int zcConfigFileHandler = Wads.FindLump( "zchecker.cfg" );

        if ( zcConfigFileHandler != -1 ) {
            zcConfigFile = Wads.ReadLump( zcConfigFileHandler );
            Array<String> zcConfigLines;

            zcConfigFile.ToLower();
			ENewlineTypes newlinesType = GetTextNewlinesType( zcConfigFile );

			switch ( newlinesType ) {
				case ZCNL_Unix: handleConfigFileNewlines( "*nix (LF)", "\n" ); break;
				case ZCNL_DOS: handleConfigFileNewlines( "DOS/Windows (CR/LF)", "\r\n" ); break;
				case ZCNL_Mac: handleConfigFileNewlines( "MacOS (CR)", "\r" ); break;
				case ZCNL_DOSReversed: handleConfigFileNewlines( "DOS reversed (LF/CR)", "\n\r" ); break;
				default:
					ZCGlobal.Log( LL_Debug, TEXTCOLOR_YELLOW .. "Cannot recognize new lines type, using *nix" .. LLCOLOR_DEBUG .. "." );
					newlinesType = ZCNL_Unix;
					break;
			}

            zcConfigFile.Replace( "\r\n", "\n" );
            zcConfigFile.Split( zcConfigLines, "\n" );

			uint curLineIndex = 1;

            for ( int i = 0; i < zcConfigLines.Size(); i++, curLineIndex++ ) {
                String curLine = zcConfigLines[ i ];

                // Comments and extra spaces delete:
                int IndexOfCommentStart = curLine.IndexOf( "#" );
                if ( IndexOfCommentStart != -1 )
                    curLine.Truncate( IndexOfCommentStart );

				curLine = ZCGlobal.TruncateStringBySpaces( curLine );

                // Try to unparse current line if it isn't empty or remove it elsewere:
                if ( curLine != "" ) {
	                Array<String> curLineWords;

					curLineWords.Clear();
		            curLine.Replace( "\t", " " );
		            curLine.Split( curLineWords, " ", TOK_SKIPEMPTY );

					int argsamount = ( curLineWords.Size() - 1 );

                    if ( curLineWords[ 0 ] == "ccmd" || curLineWords[ 0 ] == "addccmd" ) {
						if ( argsamount == 1 ) {
							if ( !addConsoleClass( curLineWords[ 1 ], false ) )
								ZCGlobal.Log( LL_Emergency, GetClassName() .. "::IncludeConsoleClasses(). Wrong ConsoleCommand class \"" .. curLineWords[ 1 ] .. "\"." );
						} else if ( argsamount == 2 && curLineWords[ 2 ] == "important" ) {
							if ( !addConsoleClass( curLineWords[ 1 ], true ) )
								ZCGlobal.Log( LL_Emergency, GetClassName() .. "::IncludeConsoleClasses(). Wrong ConsoleCommand class \"" .. curLineWords[ 1 ] .. "\"." );
						} else {
							checkKeywordArgsAmount( argsamount, curLineWords[ 0 ], 1, "class<ZCConsoleClass> [important]" );
						}

					} else if ( curLineWords[ 0 ] == "ccmdsep" || curLineWords[ 0 ] == "addccmdsep" || curLineWords[ 0 ] == "ccmdseparator" || curLineWords[ 0 ] == "addccmdseparator" ) {
						addConsoleSeparator( argsamount == 1 && curLineWords[ 1 ] == "important" );

                    } else if ( curLineWords[ 0 ] == "panel" || curLineWords[ 0 ] == "addpanel" || curLineWords[ 0 ] == "infopanel" || curLineWords[ 0 ] == "addinfopanel" ) {
						if ( checkKeywordArgsAmount( argsamount, curLineWords[ 0 ], 1, "class<ZCBaseInfoPanelPart>" ) )
							if ( !addInfoPanelClass( curLineWords[ 1 ] ) )
								ZCGlobal.Log( LL_Emergency, GetClassName() .. "::IncludeConsoleClasses(). Wrong PanelInfo class \"" .. curLineWords[ 1 ] .. "\"." );

                    } else {
						ZCGlobal.Log( LL_Emergency, GetClassName() .. "::IncludeConsoleClasses(). Unknown keyword \"" .. curLineWords[ 0 ] .. "\"." );
					}

                } else {
                    zcConfigLines.Delete( i, 1 );
                    i--;
                } // of else of if ( curLine != "" ) {}

            } // of for ( int i = 0; i < zcConfigLines.Size(); i++, curLineIndex++ ) {}
        } else {
			ZCGlobal.Log( LL_Emergency, GetClassName() .. "::IncludeConsoleClasses(). Cannot find file \"ZChecker.cfg\"." );
		} // of else of if ( zcConfigFileHandler != -1 ) {}

	} // of void IncludeConsoleClasses( void ) {}

	override void OnRegister() {
		SetOrder( ZCheckerHandlerOrder );

		ZCGlobal.Log( LL_Debug, GetClassName() .. "::OnRegister(). ZChecker initializes..." );

		for ( int i = 0; i < MaxTestee; i++ ) {
			Testee[ i ] = new( 'ZCheckerField' );
			Testee[ i ].fieldPos = i;
			Testee[ i ].actorType = IAT_Normal;
		}

		zcCCMDInfo = new( 'ZCheckerCCMDInfo' );
		zcCCMDInfo.handler = self;
		zcInfoPanelFont = Font.FindFont( "smallfont" );

		ZCGlobal.Log( LL_Debug, GetClassName() .. "::OnRegister(). Creating CCMD handlers and info panel classes..." );
		IncludeConsoleClasses();

		ZCGlobal.Log( LL_Detailed, "ZChecker initialized successfully. " .. zcConsoleHandleClasses.Size() .. " console commands, " .. zcInfoPanelClasses.Size() .. " info panels." );

		Super.OnRegister();
	}

	// Method to update all of the field meta-information:
	private clearscope Actor UpdateTesteeField( int fieldindex ) {
		Actor curTestee = NULL;

		if ( Testee[ fieldindex ] && Testee[ fieldindex ].mo )
			curTestee = Testee[ fieldindex ].mo;

		// Updating info actors type. Usually it's better not to cross the 
		//ui, data and play scopes in one method...
		if ( curTestee is "StateProvider" && PlayerPawn( Inventory( curTestee ).owner ) ) {
			Testee[ fieldindex ].mo = curTestee = PlayerPawn( Inventory( curTestee ).owner ).player.ReadyWeapon;
			Testee[ fieldindex ].actorType = IAT_StateProvider;
		} else {
			Testee[ fieldindex ].actorType = IAT_Normal;
		}

		return curTestee;
	}


	override void RenderOverlay( RenderEvent e ) {
		// User CVars locating uses a relatively long algorithm. After a 
		//while I'll optimize them and move somewhere.
		Array<ZCBaseInfoPanelPart> infoPanelParts;
		PlayerInfo pl = players[ consoleplayer ];

		for ( int iCurInfoPanel = 0; iCurInfoPanel < zcInfoPanelClasses.Size(); iCurInfoPanel++ ) {
			ZCBaseInfoPanelPart panelpart = zcInfoPanelClasses[ iCurInfoPanel ];
			CVar curInfoPanelCVar = CVar.GetCVar( panelpart.optionCVarName, pl );

			if ( !curInfoPanelCVar ) {
				ZCGlobal.ClearscopeLog( LL_Emergency, "Info panel class \"" .. TEXTCOLOR_RED .. panelpart.GetClassName() .. LLCOLOR_EMERGENCY .. "\" refers to unknown user CVar \"" .. TEXTCOLOR_RED .. panelpart.optionCVarName .. LLCOLOR_EMERGENCY .."\"." );
			} else if ( curInfoPanelCVar.GetBool() ) {
				infoPanelParts.Push( panelpart );
			}
		}

		double infopanelStartX = CVar.FindCVar( "mcm_zchecker_infopanel_startx" ).GetFloat();
		double infopanelStartY = CVar.FindCVar( "mcm_zchecker_infopanel_starty" ).GetFloat();
		double infopanelScale = CVar.FindCVar( "mcm_zchecker_infopanel_scale" ).GetFloat();
		double infopanelOffsetX = CVar.FindCVar( "mcm_zchecker_infopanel_offsetx" ).GetFloat();


		for ( int i = 0; i < MaxTestee; i++ ) {
			Actor curTestee = UpdateTesteeField( i );

			if ( !curTestee )
				continue;


			// Forming a full information panel:
			String outStr = "";

			for ( int j = 0; j < infoPanelParts.Size(); j++ )
				outStr = outStr .. "\c-" .. infoPanelParts[ j ].GetInfo( curTestee, Testee[ i ] ) .. "\n";


			// Thanks to m8f for the "drawTextCenter()" function code from its "Hellscape Navigator".
			double bordersXMargin = 4.0;
			double scale = 1.0 / infopanelScale;
			double x = infopanelStartX * 0.01 + i * infopanelOffsetX * 0.01;
			double y = infopanelStartY * 0.01;

			int width = int( scale * Screen.GetWidth() );
			int height = int( scale * Screen.GetHeight() );
			double stringWidth = zcInfoPanelFont.StringWidth( outStr );

			x = ( width * x ); //- stringWidth / 2;
			y *= height;

			// Push the text out from the borders:
			if ( x < bordersXMargin )
				x = bordersXMargin;
			else if ( x > width - stringWidth - bordersXMargin )
				x = width - stringWidth - bordersXMargin;

			Screen.DrawText( zcInfoPanelFont, Font.CR_GRAY, x, y, outStr,
							DTA_KeepRatio, true, DTA_VirtualWidth, width, DTA_VirtualHeight, height );
		} // of for ( int i = 0; i < MaxTestee; i++ ) {}

		Super.RenderOverlay( e );
	} // of override void RenderOverlay( RenderEvent e ) {}


	override void NetworkProcess( ConsoleEvent e ) {
		// ZChecker CCMDs parsing:
		String ccmdName = zcCCMDInfo.InitFromEvent( e );

		if ( ZCGlobal.Get().zcCurLogLevel >= LL_Debug ) {
			ZCGlobal.Log( LL_Debug, GetClassName() .. "::NetworkProcess(). CCMD name: " .. ccmdName .. ". Has " .. zcConsoleHandleClasses.Size() .. " available commands." );

			for ( int i = 0; i < zcCCMDInfo.args.Size(); i++ )
				ZCGlobal.Log( LL_Debug, GetClassName() .. "::NetworkProcess(). CCMD arg " .. i + 1 .. ": " .. zcCCMDInfo.args[ i ] );
		}

		// Polling ZChecker CCMD handlers by stored netevent name:
		for ( int i = 0; i < zcConsoleHandleClasses.Size(); i++ ) {
			if ( !( zcConsoleHandleClasses[ i ] is 'ZCConsoleCommand' ) )
				continue;

			ZCConsoleCommand curCCMD = ZCConsoleCommand( zcConsoleHandleClasses[ i ] );

			if ( ccmdName ~== curCCMD.GetCommand() ) {
				curCCMD.ccmd = zcCCMDInfo;

				if ( curCCMD.HandleArguments() ) {
					ZCGlobal.Log( LL_Debug, GetClassName() .. "::NetworkProcess(). " .. curCCMD.ccmd.args.Size() .. " argument(s) for CCMD " .. curCCMD.GetClassName() .. " handled successfully." );
					curCCMD.DoCommand();
				} else {
					ZCGlobal.Log( LL_Normal | LL_NoPrefix, TEXTCOLOR_YELLOW .. "Usage\c-:\n" .. curCCMD.GetHelpString() );
				}

				break;
			}
		} // of for ( int i = 0; i < zcConsoleHandleClasses.Size(); i++ ) {}

		Super.NetworkProcess( e );
	} // of override void NetworkProcess( ConsoleEvent e ) {}


	override void WorldTick() {
		if ( ( gamestate == GS_LEVEL || gamestate == GS_DEMOSCREEN ) && !( level.maptime % 5 ) )
			ZCGlobal.UpdateLogLevel();

		Super.WorldTick();
	} // of override void WorldTick() {}

} // of class ZCheckerHandler: EventHandler {}
