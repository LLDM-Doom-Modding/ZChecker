// Not all parameters normally accessible via ZScript...

// [McM] Compile(d) via gdcc-acc v0.13.2.

#include "zcommon.acs"

// Log levels enum:
#define LL_Emergency 0
#define LL_General 1
#define LL_Detailed 2
#define LL_Developer 3
#define LL_Debug 4

#define LL_Common LL_General
#define LL_Normal LL_General
#define LL_Main LL_General
#define LL_Extended LL_Detailed
#define LL_Details LL_Detailed
#define LL_Extra LL_Developer
#define LL_Development LL_Developer
#define LL_Interface LL_Developer
#define LL_APIMessage LL_Developer

#define LL_FlagsMask 0xFFF0
#define LL_NoLocalize 0x2000
#define LL_NoDot 0x4000
#define LL_NoPrefix 0x8000


// For saves compatibility between different ZChecker versions, the sum 
//of (data + extra alignment) should always be equal to 8192 bytes:
int alignbuffermain[ 1508 ] = { 1 };	// "len * sizeof( int32_t )" = "len * 4";
str alignbufferoffset = "aa";			// "len * sizeof( char ) + 1" = "len + 1".



script "ZChecker_CCMD_SetState" ( void ) {
	str newStateName = ScriptCall( "ZCSetStateCommand", "ACS_GetStateName" );

	if ( SetActorState( 0, newStateName, true ) ) {
		ScriptCall( "ZCGlobal", "Log", LL_Detailed, StrParam( s:"State \"", s:newStateName, s:"\" for actor ", s:GetActorClass( 0 ), s:" is succesfully set." ) );
	} else {
		ScriptCall( "ZCGlobal", "Log", LL_Main, StrParam( s:"Cannot set state \"", s:newStateName, s:"\" for actor ", s:GetActorClass( 0 ) ) );
	}
}

// Argument: "-1" is a toggle, "0"/"1" is a reset/set.
script "ZChecker_CCMD_SetFlag" ( int setType ) {
	str flagName = ScriptCall( "ZCSetFlagCommand", "ACS_GetFlagName" );
	int newValue = setType;

	newValue = ( setType != -1? setType : !CheckFlag( 0, flagName ) );

	if ( SetActorFlag( 0, flagName, newValue ) ) {
		ScriptCall( "ZCGlobal", "Log", LL_Detailed, StrParam( s:"Flag \"", s:flagName, s:"\" for actor ", s:GetActorClass( 0 ), s:" is succesfully set to ", i:newValue ) );
	}
}

//  Ancient ZScript bug fixing.
// https://forum.zdoom.org/viewtopic.php?f=2&t=62501
//  [McM]: suddenly CVar name + CVar value cannot store more than 254 symbols.
script "ZChecker_SetUserCVarStringMeta" ( int playernum ) {
	str cvarname = GetUserCVarString( playernum, "lldm_zcinternal_metastring" );
	str data = GetUserCVarString( playernum, cvarname );

    SetUserCVarString( playernum, cvarname, data );
}

script "ZChecker_SetInfopanelPartCVar" ( int playernum, int infopanelPart ) {
	str cvarname = StrParam( s:"lldm_zcinternal_panelsorder_part", i:infopanelPart );
	str data = GetUserCVarString( playernum, cvarname );

	SetUserCVarString( playernum, cvarname, data );
}

script "ZChecker_SetExternalInterfaceCVar" ( int playernum ) {
	str cvarname = "lldm_zcexternal";
	int data = GetUserCVar( playernum, cvarname );

	SetUserCVar( playernum, cvarname, data );
}

script "ZChecker_WarpToEverything" ( int simplified_level ) {
	if ( simplified_level )
		ChangeLevel( "Everything_simple", 0, CHANGELEVEL_NOINTERMISSION, -1 );
	else
		ChangeLevel( "Everything_alternative", 0, CHANGELEVEL_NOINTERMISSION, -1 );
}
